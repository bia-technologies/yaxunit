//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2024 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("ЭтоСистемноеПеречисление")
			.СПараметрами(Тип("ВидДвиженияБухгалтерии"), Истина)
			.СПараметрами(Тип("Дата"), Ложь)
			.СПараметрами(Тип("ПеречислениеСсылка.PushУведомления"), Ложь)
		.ДобавитьТест("ТипыСистемныхПеречислений")
		.ДобавитьТест("ИмяСистемногоПеречисления")
			.СПараметрами(Тип("ВидДвиженияБухгалтерии"), "ВидДвиженияБухгалтерии")
		.ДобавитьТест("ЭтоПримитивныйТип")
			.СПараметрами(Тип("ПеречислениеСсылка.PushУведомления"), Ложь)
			.СПараметрами(Тип("Строка"), Истина)
			.СПараметрами(Тип("Число"), Истина)
			.СПараметрами(Тип("Неопределено"), Ложь)
			.СПараметрами(Новый ОписаниеТипов("УникальныйИдентификатор"), Истина)
			.СПараметрами(Новый ОписаниеТипов("Число, Дата"), Истина)
			.СПараметрами(Новый ОписаниеТипов("Число, ПеречислениеСсылка.PushУведомления, Дата"), Ложь)
		.ДобавитьТест("ЭтоКоллекцияПримитивныхТипов")
			.СПараметрами(ЮТКоллекции.ЗначениеВМассиве(Тип("Строка"), Тип("Дата")), Истина)
			.СПараметрами(ЮТКоллекции.ЗначениеВМассиве(Тип("Строка"), Тип("Дата"), Тип("ПеречислениеСсылка.PushУведомления")), Ложь)
		.ДобавитьТест("ИдентификаторТипа")
			.СПараметрами("Дата")
			.СПараметрами("Строка")
			.СПараметрами("Число")
			.СПараметрами("Булево")
			.СПараметрами("Неопределено")
			.СПараметрами("Null")
			.СПараметрами("УникальныйИдентификатор")
			.СПараметрами("ВидДвиженияБухгалтерии")
			.СПараметрами("HTTPЗапрос")
			.СПараметрами("ТабличныйДокумент")
			.СПараметрами("ХранилищеЗначения")
			.СПараметрами("ПеречислениеСсылка.PushУведомления")
		.ДобавитьСерверныйТест("ИдентификаторТипа")
			.СПараметрами("СправочникМенеджер.Банки")
			.СПараметрами("СхемаКомпоновкиДанных")
			.СПараметрами("HTTPСервисЗапрос")
			.СПараметрами("ОбъектXDTO")
			.СПараметрами("ОбъектМетаданных")
	;
	
КонецПроцедуры

Процедура ЭтоСистемноеПеречисление(Тип, ОжидаемыйРезультат) Экспорт
	
	ЮТест.ОжидаетЧто(ЮТТипыДанныхСлужебный.ЭтоСистемноеПеречисление(Тип))
		.Равно(ОжидаемыйРезультат);
КонецПроцедуры

Процедура ТипыСистемныхПеречислений() Экспорт
	
	ЮТест.ОжидаетЧто(ЮТТипыДанныхСлужебный.ТипыСистемныхПеречислений())
		.ИмеетТип("ОписаниеТипов")
		.Что(ЮТТипыДанныхСлужебный.ТипыСистемныхПеречислений().Типы())
		.Содержит(Тип("ВидДвиженияБухгалтерии"))
		.Содержит(Тип("ИспользованиеАгрегатаРегистраНакопления"));
	
КонецПроцедуры

Процедура ИмяСистемногоПеречисления(Тип, ОжидаемыйРезультат) Экспорт
	
	ЮТест.ОжидаетЧто(ЮТТипыДанныхСлужебный.ИмяСистемногоПеречисления(Тип))
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ЭтоПримитивныйТип(Тип, ОжидаемыйРезультат) Экспорт
	
	ЮТест.ОжидаетЧто(ЮТТипыДанныхСлужебный.ЭтоПримитивныйТип(Тип))
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ЭтоКоллекцияПримитивныхТипов(Тип, ОжидаемыйРезультат) Экспорт
	
	ЮТест.ОжидаетЧто(ЮТТипыДанныхСлужебный.ЭтоКоллекцияПримитивныхТипов(Тип))
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ИдентификаторТипа(ИмяТипа) Экспорт
	
	Тип = Тип(ИмяТипа);
	Результат = ЮТТипыДанныхСлужебный.ИдентификаторТипа(Тип(ИмяТипа));
	ЮТест.ОжидаетЧто(Результат)
		.Заполнено();
	
	ЮТест.ОжидаетЧто(Тип(Результат))
		.Равно(Тип);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
