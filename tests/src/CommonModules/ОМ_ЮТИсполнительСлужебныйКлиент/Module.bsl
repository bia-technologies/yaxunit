//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("ОбработчикСохранитьКодВозврата")
		.ДобавитьТест("ОбработчикЗагрузитьТестыВнешнейОбработки")
	;
		
КонецПроцедуры

Процедура ОбработчикСохранитьКодВозврата() Экспорт
	
	ФайлРезультата = ЮТест.Данные().НовоеИмяВременногоФайла("txt");
	
	ДополнительныеПараметры = ДополнительныеПараметрыТестирования(ФайлРезультата, МассивОшибок());
	ЮТИсполнительСлужебныйКлиент.ОбработчикСохранитьКодВозврата(Неопределено, ДополнительныеПараметры);
	
	ПроверитьРезультатВыполнения(ФайлРезультата, "0");
	
	ДополнительныеПараметры = ДополнительныеПараметрыТестирования(ФайлРезультата, МассивОшибок(Истина));
	ЮТИсполнительСлужебныйКлиент.ОбработчикСохранитьКодВозврата(Неопределено, ДополнительныеПараметры);
	
	ПроверитьРезультатВыполнения(ФайлРезультата, "1");
КонецПроцедуры

Процедура ОбработчикЗагрузитьТестыВнешнейОбработки() Экспорт
	
	ПараметрыИсполнения = ПараметрыИсполнения();
	ПараметрыИсполнения.Вставить("ИмяВнешнейОбработки", "");
	ПараметрыИсполнения.Вставить("ОписаниеМодуля", ЮТИсполнительСлужебныйКлиент.ОписаниеВременногоМодуля());
	ПараметрыИсполнения.ОписаниеМодуля.Текст =
	"Процедура ИсполняемыеСценарии() Экспорт
	|
	|	ЮТТесты
	|		.ДобавитьТест(""ТестУспешно"")
	|		.ДобавитьТест(""ТестОшибка"")
	|		.ДобавитьТест(""ТестСломан"")
	|	;
	|
	|КонецПроцедуры
	|
	|Процедура ТестУспешно() Экспорт
	|	ЮТест.ОжидаетЧто(1).Равно(1);
	|КонецПроцедуры
	|
	|Процедура ТестОшибка() Экспорт
	|	ЮТест.ОжидаетЧто(1).Равно(2);
	|КонецПроцедуры
	|
	|Процедура ТестСломан() Экспорт
	|	ЮТест.ОжидаетЧто(1).ОтсутствующийМетод(2);
	|КонецПроцедуры
	|";
	
	ПараметрыИсполнения.ОписаниеМодуля.КлиентУправляемое = Истина;
	ПараметрыИсполнения.ОписаниеМодуля.КлиентОбычное = Истина;
	ПараметрыИсполнения.ОписаниеМодуля.Сервер = Истина;
	
	ДобавитьОбработчикЦепочки(ПараметрыИсполнения, "ОбработчикСоздатьВнешнююОбработку");
	ДобавитьОбработчикЦепочки(ПараметрыИсполнения, "ОбработчикЗагрузитьТестыВнешнейОбработки");
	ПараметрыИсполнения.Цепочка.Добавить(ЮТАсинхроннаяОбработкаСлужебныйКлиент.НовыйПустойОбработчик(2));
	
	ЮТАсинхроннаяОбработкаСлужебныйКлиент.ВызватьСледующийОбработчик(ПараметрыИсполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыИсполнения()
	
	ПараметрыИсполнения = ЮТИсполнительСлужебныйКлиент.ПараметрыИсполнения();
	ПараметрыИсполнения.АргументыЗапуска = "";
	ПараметрыИсполнения.ИсполняемыеТестовыеМодули = Новый Массив;
	
	ПараметрыИсполнения.ПараметрыЗапуска = Новый Структура;
	ПараметрыИсполнения.РезультатыТестирования = Новый Массив;
	
	Возврат ПараметрыИсполнения;
	
КонецФункции

Функция ДополнительныеПараметрыТестирования(ИмяФайлаРезультата, МассивОшибок)
	
	Тесты = Тесты(МассивОшибок);
	НаборыТестов = НаборыТестов(Тесты);
	ОписаниеРезультата = ОписаниеРезультатаТестирования(НаборыТестов);
	
	ДополнительныеПараметры = ПараметрыИсполнения();
	ДополнительныеПараметры.Цепочка.Добавить(ЮТАсинхроннаяОбработкаСлужебныйКлиент.НовыйПустойОбработчик(2));
	
	ДополнительныеПараметры.ПараметрыЗапуска.Вставить("exitCode", ИмяФайлаРезультата);
	ДополнительныеПараметры.РезультатыТестирования.Добавить(ОписаниеРезультата);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция МассивОшибок(ДобавлятьУпавшийТест = Ложь)
	Ошибки = Новый Массив;
	ОписаниеОшибки = ЮТФабрикаСлужебный.ОписаниеВозникшейОшибки("Метод пропущен");
	ОписаниеОшибки.ТипОшибки = ЮТФабрикаСлужебный.ТипыОшибок().Пропущен;
	Ошибки.Добавить(ОписаниеОшибки);
	
	Если ДобавлятьУпавшийТест Тогда
		ОписаниеОшибки = ЮТФабрикаСлужебный.ОписаниеВозникшейОшибки("Ошибка исполнения");
		ОписаниеОшибки.ТипОшибки = ЮТФабрикаСлужебный.ТипыОшибок().Исполнения;
		Ошибки.Добавить(ОписаниеОшибки);
	КонецЕсли;
	
	Возврат Ошибки;
КонецФункции

Функция Тесты(МассивОшибок)
	Тесты = Новый Массив;
	Тесты.Добавить(Новый Структура("Ошибки", МассивОшибок));
	
	Возврат Тесты;
КонецФункции

Функция НаборыТестов(Тесты)
	НаборыТестов = Новый Массив;
	НаборыТестов.Добавить(Новый Структура("Тесты, Ошибки", Тесты, Новый Массив));
	
	Возврат НаборыТестов;
КонецФункции

Функция ОписаниеРезультатаТестирования(НаборыТестов)
	
	ОписаниеРезультата = Новый Структура;
	ОписаниеРезультата.Вставить("Метаданные", Неопределено);
	ОписаниеРезультата.Вставить("НаборыТестов", НаборыТестов);
	ОписаниеРезультата.Вставить("Ошибки", Новый Массив);
	ОписаниеРезультата.Вставить("НастройкиВыполнения", Неопределено);
	
	Возврат ОписаниеРезультата;
	
КонецФункции

Процедура ПроверитьРезультатВыполнения(ИмяФайлаРезультата, ОжидаемыйРезультат)
	РезультатВыполнения = СокрЛП(ЮТФайлы.ДанныеТекстовогоФайла(ИмяФайлаРезультата));
	ЮТест.ОжидаетЧто(РезультатВыполнения)
		.ИмеетТип("Строка")
		.Равно(ОжидаемыйРезультат)
	;
КонецПроцедуры

Процедура ДобавитьОбработчикЦепочки(ПараметрыИсполнения, ИмяМетода)
	
	Обработчик = Новый ОписаниеОповещения(ИмяМетода, ЮТИсполнительСлужебныйКлиент, ПараметрыИсполнения, "ОбработчикОшибки", ЮТИсполнительСлужебныйКлиент);
	ПараметрыИсполнения.Цепочка.Добавить(Обработчик);
	
КонецПроцедуры

#КонецОбласти
