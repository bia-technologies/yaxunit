
//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Если Сервер Тогда

#Область ОписаниеПеременных

// Содержит объект защищенного соединения для осуществления HTTPS-соединения.
Перем ЗащищенноеСоединение Экспорт;
Перем SecureConnection Экспорт; // ENG

// Содержит текущее значение использования аутентификации NTLM или Negotiate на сервере.
Перем ИспользоватьАутентификациюОС Экспорт;
Перем UseOSAuthentication Экспорт; // ENG

// Пароль пользователя, от имени которого установлено соединение.
Перем Пароль Экспорт;
Перем Password Экспорт; // ENG

// Пользователь, от имени которого установлено соединение.
Перем Пользователь Экспорт;
Перем User Экспорт; // ENG

// Порт сервера, с которым произведено соединение.
Перем Порт Экспорт;
Перем Port Экспорт; // ENG

// Прокси, через который установлено соединение.
Перем Прокси Экспорт;
Перем Proxy Экспорт; // ENG

// Сервер, с которым произведено соединение.
Перем Сервер Экспорт;
Перем Host Экспорт; // ENG

// Определяет время ожидания осуществляемого соединения и операций, в секундах.
Перем Таймаут Экспорт;
Перем Timeout Экспорт; // ENG

// Контекст конструктора
Перем Контекст;

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область HTTPСоединение

// Отправляет данные на указанный адрес для обработки с использованием указанного HTTP-метода (ENG).
//
// Параметры:
//  HTTPМетод - Строка - Строка, содержащая имя HTTP-метода для запроса
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//  ИмяВыходногоФайла - Строка - Имя файла, в который записывается тело ответа
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция CallHTTPMethod(HTTPМетод, HTTPЗапрос, ИмяВыходногоФайла = "") Экспорт
	
	Возврат ВызватьHTTPМетод(HTTPМетод, HTTPЗапрос, ИмяВыходногоФайла);
	
КонецФункции

// Отправляет данных на HTTP-сервер с помощью PUT-запроса (ENG).
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Put(HTTPЗапрос) Экспорт
	
	Возврат Записать(HTTPЗапрос);
	
КонецФункции

// Изменяет данные на сервере при помощи PATCH-запроса (ENG).
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Patch(HTTPЗапрос) Экспорт
	
	Возврат Изменить(HTTPЗапрос);
	
КонецФункции

// Отправляет данных на HTTP-сервер с помощью POST-запроса (ENG).
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//  ИмяВыходногоФайла - Строка - Имя файла, в который записывается тело ответа
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Post(HTTPЗапрос, ИмяВыходногоФайла = "") Экспорт
	
	Возврат ОтправитьДляОбработки(HTTPЗапрос, ИмяВыходногоФайла);
	
КонецФункции

// Получает данные с HTTP-сервера, используя GET-запрос (ENG).
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//  ИмяВыходногоФайла - Строка - Имя файла, в который записывается тело ответа
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Get(HTTPЗапрос, ИмяВыходногоФайла = "") Экспорт
	
	Возврат  Получить(HTTPЗапрос, ИмяВыходногоФайла);
	
КонецФункции

// Получает при помощи HEAD-запроса информацию о запрашиваемых данных, содержащуюся в заголовках, не получая сами данные (ENG).
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Head(HTTPЗапрос) Экспорт
	
	Возврат ПолучитьЗаголовки(HTTPЗапрос);
	
КонецФункции

// Удаляет ресурс на сервере при помощи HTTP-запроса DELETE (ENG).
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Delete(HTTPЗапрос) Экспорт
	
	Возврат Удалить(HTTPЗапрос);
	
КонецФункции

// Отправляет данные на указанный адрес для обработки с использованием указанного HTTP-метода.
//
// Параметры:
//  HTTPМетод - Строка - Строка, содержащая имя HTTP-метода для запроса
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//  ИмяВыходногоФайла - Строка - Имя файла, в который записывается тело ответа
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция ВызватьHTTPМетод(HTTPМетод, HTTPЗапрос, ИмяВыходногоФайла = "") Экспорт
	
	Если Контекст.ВыброситьИсключение Тогда
		ВызватьИсключение(Контекст.ТекстИсключения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяВыходногоФайла) Тогда
		ДвоичныеДанные = Контекст.HTTPОтвет.ПолучитьТелоКакДвоичныеДанные();
		ДвоичныеДанные.Записать(ИмяВыходногоФайла);
	КонецЕсли;
	
	Возврат Контекст.HTTPОтвет;
	
КонецФункции

// Отправляет данных на HTTP-сервер с помощью PUT-запроса.
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Записать(HTTPЗапрос) Экспорт
	
	Возврат ВызватьHTTPМетод("PUT", HTTPЗапрос);
	
КонецФункции

// Изменяет данные на сервере при помощи PATCH-запроса.
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Изменить(HTTPЗапрос) Экспорт
	
	Возврат ВызватьHTTPМетод("PATCH", HTTPЗапрос);
	
КонецФункции

// Отправляет данных на HTTP-сервер с помощью POST-запроса.
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//  ИмяВыходногоФайла - Строка - Имя файла, в который записывается тело ответа
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция ОтправитьДляОбработки(HTTPЗапрос, ИмяВыходногоФайла = "") Экспорт
	
	Возврат ВызватьHTTPМетод("POST", HTTPЗапрос, ИмяВыходногоФайла);
	
КонецФункции

// Получает данные с HTTP-сервера, используя GET-запрос.
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//  ИмяВыходногоФайла - Строка - Имя файла, в который записывается тело ответа
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Получить(HTTPЗапрос, ИмяВыходногоФайла = "") Экспорт
	
	Возврат ВызватьHTTPМетод("GET", HTTPЗапрос, ИмяВыходногоФайла);
	
КонецФункции

// Получает при помощи HEAD-запроса информацию о запрашиваемых данных, содержащуюся в заголовках, не получая сами данные.
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция ПолучитьЗаголовки(HTTPЗапрос) Экспорт
	
	Возврат ВызватьHTTPМетод("HEAD", HTTPЗапрос);
	
КонецФункции

// Удаляет ресурс на сервере при помощи HTTP-запроса DELETE.
//
// Параметры:
//  HTTPЗапрос - HTTPЗапрос - Запрос, передаваемый на сервер
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Ответа HTTP-сервера на запрос
//
Функция Удалить(HTTPЗапрос) Экспорт
	
	Возврат ВызватьHTTPМетод("DELETE", HTTPЗапрос);
	
КонецФункции

#КонецОбласти

#Область Конструктор

// Указывает, что при выполнении HTTP-запроса необходимо выбросить исключение.
//
// Параметры:
//  ТекстИсключения - Строка - Текст выбрасываемого исключения
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPСоединение - Конструктор
//
Функция ВыброситьИсключение(ТекстИсключения) Экспорт
	
	Контекст.ВыброситьИсключение = Истина;
	Контекст.ТекстИсключения = ТекстИсключения;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает возвращаемый HTTP-ответ.
//
// Параметры:
//  HTTPОтвет - ОбработкаОбъект.ЮТHTTPОтвет, HTTPОтвет - Возвращаемый HTTP-ответ
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPСоединение - Конструктор
//
Функция УстановитьHTTPОтвет(HTTPОтвет) Экспорт
	
	Контекст.HTTPОтвет = HTTPОтвет;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает защищенное соединение.
//
// Параметры:
//  Значение - ЗащищенноеСоединениеOpenSSL - Объект защищенного соединения для осуществления HTTPS соединения
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет - Конструктор
//
Функция УстановитьЗащищенноеСоединение(Значение) Экспорт
	
	ЗащищенноеСоединение = Значение;
	SecureConnection = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить использование аутентификации ОС.
//
// Параметры:
//  Значение - Булево - Включает использование аутентификации NTLM или Negotiate на сервере
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет - Конструктор
//
Функция УстановитьАутентификациюОС(Значение) Экспорт
	
	ИспользоватьАутентификациюОС = Значение;
	UseOSAuthentication = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает пароль для аутентификации.
//
// Параметры:
//  Значение - Строка - Пароль пользователя на указанном сервере
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет - Конструктор
//
Функция УстановитьПароль(Значение) Экспорт
	
	Пароль = Значение;
	Password = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает пользователя для аутентификации.
//
// Параметры:
//  Значение - Строка - Имя пользователя на указанном сервере
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет - Конструктор
//
Функция УстановитьПользователя(Значение) Экспорт
	
	Пользователь = Значение;
	User = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить порт соединения.
//
// Параметры:
//  Значение - Число - Порт сервера, с которым осуществляется соединение
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет - Конструктор
//
Функция УстановитьПорт(Значение) Экспорт
	
	Порт = Значение;
	Port = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает прокси соединения.
//
// Параметры:
//  Значение - ИнтернетПрокси - Прокси, используемый для соединения с сервером
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPОтвет - Конструктор
//
Функция УстановитьПрокси(Значение) Экспорт
	
	Прокси = Значение;
	Proxy = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает адрес сервера.
//
// Параметры:
//  Значение - Строка - Хост сервера, с которым осуществляется соединение
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPСоединение - Конструктор
//
Функция УстановитьСервер(Значение) Экспорт
	
	Сервер = Значение;
	Host = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Устанавливает таймаут соединения.
//
// Параметры:
//  Значение - Число - Таймаут осуществляемого соединения и операций, в секундах
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPСоединение - Конструктор
//
Функция УстановитьТаймаут(Значение) Экспорт
	
	Таймаут = Значение;
	Timeout = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

// Конструктор по умолчанию.
//
// Параметры:
//  _Сервер - Строка - Хост сервера, с которым осуществляется соединение
//  _Порт - Число - Порт сервера, с которым осуществляется соединение
//  _Пользователь - Строка - Имя пользователя на указанном сервере
//  _Пароль - Строка - Пароль пользователя на указанном сервере
//  _Прокси - ИнтернетПрокси, Неопределено - Прокси, используемый для соединения с сервером
//  _Таймаут - Число - Таймаут осуществляемого соединения и операций, в секундах.
//  _ЗащищенноеСоединение - ЗащищенноеСоединениеOpenSSL, Неопределено - Объект защищенного соединения для осуществления HTTPS соединения
//  _ИспользоватьАутентификациюОС - Булево - Включает использование аутентификации NTLM или Negotiate на сервере
//
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPСоединение - Конструктор
//
Функция КонструкторПоУмолчанию(_Сервер, _Порт = 0, _Пользователь = "", _Пароль = "",
			_Прокси = Неопределено, _Таймаут = 0, _ЗащищенноеСоединение = Неопределено,
			_ИспользоватьАутентификациюОС = Ложь) Экспорт
	
	УстановитьАутентификациюОС(_ИспользоватьАутентификациюОС);
	УстановитьЗащищенноеСоединение(_ЗащищенноеСоединение);
	УстановитьПароль(_Пароль);
	УстановитьПользователя(_Пользователь);
	УстановитьСервер(_Сервер);
	УстановитьТаймаут(_Таймаут);
	
	Если _Прокси = Неопределено Тогда
		ПроксиПоУмолчанию = Новый ИнтернетПрокси;
		УстановитьПрокси(ПроксиПоУмолчанию);
	Иначе
		УстановитьПрокси(_Прокси);
	КонецЕсли;
	
	Если _Порт = 0 Тогда
		ПортПоУмолчанию = ?(ЗащищенноеСоединение = Неопределено, 80, 443);
		УстановитьПорт(ПортПоУмолчанию);
	Иначе
		УстановитьПорт(_Порт);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Инициализирует начальные значения переменных.
//
Процедура Инициализировать()
	
	Контекст = Новый Структура;
	Контекст.Вставить("HTTPОтвет");
	Контекст.Вставить("ВыброситьИсключение", Ложь);
	Контекст.Вставить("ТекстИсключения", "");
	
	КонструкторПоУмолчанию("");
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

Инициализировать();

#КонецОбласти

#КонецЕсли
