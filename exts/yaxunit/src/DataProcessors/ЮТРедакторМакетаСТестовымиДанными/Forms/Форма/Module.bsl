#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактированиеТабличногоДокумента = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьРедактированиеТабличногоДокумента(Команда)
	
	КонвертироватьВТабличныйДокумент();
	
	РедактированиеТабличногоДокумента = Истина;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьРедактированиеТекстовогоДокумента(Команда)
	
	КонвертироватьВТекстовыйДокумент();
	
	РедактированиеТабличногоДокумента = Ложь;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТабличныйДокумент(Команда)
	
	НачатьЧтениеТабличногоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТекстовыйДокумент(Команда)
	
	НачатьЗаписьТекстовогоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТекстовыйДокумент(Команда)
	
	НачатьЧтениеТекстовогоДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ВключитьРедактированиеТабличногоДокумента.Пометка = Форма.РедактированиеТабличногоДокумента;
	Элементы.ВключитьРедактированиеТекстовогоДокумента.Пометка = Не Форма.РедактированиеТабличногоДокумента;
	
	Если Форма.РедактированиеТабличногоДокумента Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТабличногоДокумента;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТекстовогоДокумента;
	КонецЕсли;
	
	Элементы.ГруппаКомандТабличногоДокумента.Видимость = Форма.РедактированиеТабличногоДокумента;
	Элементы.ГруппаКомандТекстовогоДокумента.Видимость = Не Форма.РедактированиеТабличногоДокумента;
	
КонецПроцедуры

&НаСервере
Процедура КонвертироватьВТабличныйДокумент()
	
	МакетТабличногоДокумента = Обработки.ЮТРедакторМакетаСТестовымиДанными.КонвертироватьВТабличныйДокумент(
		МакетТекстовогоДокумента);
	
КонецПроцедуры

&НаСервере
Процедура КонвертироватьВТекстовыйДокумент()
	
	МакетТекстовогоДокумента = Обработки.ЮТРедакторМакетаСТестовымиДанными.КонвертироватьВТекстовыйДокумент(
		МакетТабличногоДокумента);
	
КонецПроцедуры

#Область ЧтениеТабличногоДокумента

&НаКлиенте
Процедура НачатьЧтениеТабличногоДокумента()
	
	Фильтры = Новый Массив();
	Фильтры.Добавить(НСтр("ru = 'Табличный документ'") + "|*.mxl");
	Фильтры.Добавить(НСтр("ru = 'Лист Excel'") + "|*.xls");
	Фильтры.Добавить(НСтр("ru = 'Лист Excel2007-...'") + "|*.xlsx");
	Фильтры.Добавить(НСтр("ru = 'Электронная таблица ODF'") + "|*.ods");
	
	//@skip-check bsl-legacy-check-dynamic-feature-access
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = СтрСоединить(Фильтры, "|");
	Диалог.ИндексФильтра = 0;
	Диалог.МножественныйВыбор = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ПродолжитьВыборФайлаТабличногоДокумента", ЭтотОбъект);
	
	Диалог.Показать(Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьВыборФайлаТабличногоДокумента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПродолжитьЧтениеТабличногоДокумента", ЭтотОбъект);
	
	НачатьПомещениеФайла(Обработчик, , ВыбранныеФайлы[0], Ложь, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЧтениеТабличногоДокумента(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	ВыбранныйФайл = Новый Файл(ВыбранноеИмяФайла);
	
	ПрочитатьТабличныйДокументНаСервере(Адрес, ВыбранныйФайл.Расширение);
	
	УдалитьИзВременногоХранилища(Адрес);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТабличныйДокументНаСервере(АдресФайла, РасширениеФайла)
	
	ВременныйФайл = ПолучитьИмяВременногоФайла(РасширениеФайла);
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);  // ДвоичныеДанные
	ДанныеФайла.Записать(ВременныйФайл);
	
	МакетТабличногоДокумента.Прочитать(ВременныйФайл);
	
	УдалитьФайлы(ВременныйФайл);
	
КонецПроцедуры

#КонецОбласти

//@skip-check bsl-legacy-check-dynamic-feature-access
//@skip-check bsl-legacy-check-type-environments
#Область ЗаписьТекстовогоДокумент

&НаКлиенте
Процедура НачатьЗаписьТекстовогоДокумента()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = НСтр("ru = 'Текстовый документ'") + "|*.txt";
	Диалог.ИндексФильтра = 0;
	
	Обработчик = Новый ОписаниеОповещения("ПродолжитьЗаписьТекстовогоДокумента", ЭтотОбъект);
	
	Диалог.Показать(Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗаписьТекстовогоДокумента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьТекстовыйДокументНаКлиенте(ВыбранныеФайлы[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТекстовыйДокументНаКлиенте(ИмяФайла)
		
	Писатель = Новый ЗаписьТекста();
	Писатель.Открыть(ИмяФайла);
	Писатель.ЗаписатьСтроку(МакетТекстовогоДокумента);
	Писатель.Закрыть();
	
КонецПроцедуры

#КонецОбласти

//@skip-check bsl-legacy-check-dynamic-feature-access
//@skip-check bsl-legacy-check-type-environments
#Область ЧтениеТекстовогоДокумента

&НаКлиенте
Процедура НачатьЧтениеТекстовогоДокумента()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = НСтр("ru = 'Текстовый документ'") + "|*.txt";
	Диалог.ИндексФильтра = 0;
	
	Обработчик = Новый ОписаниеОповещения("ПродолжитьЧтениеТекстовогоДокумента", ЭтотОбъект);
	
	Диалог.Показать(Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЧтениеТекстовогоДокумента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьТекстовыйДокументНаКлиенте(ВыбранныеФайлы[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьТекстовыйДокументНаКлиенте(ИмяФайла)
	
	Читатель = Новый ЧтениеТекста();
	Читатель.Открыть(ИмяФайла);
	МакетТекстовогоДокумента = Читатель.Прочитать();
	Читатель.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти