//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает случайный номер банковского счета, по умолчанию
// Подробности алгоритма можно прочитать по ссылке
// http://keysystems.ru/files/fo/arm_budjet/show_docum/BKS/onlinehelphtm/ro_kr_algor_klyuch_rs.htm
// Параметры:
//  БИК - Строка - БИК банка
//  КодВалюты - Строка - Код валюты
//  ЮрЛицо - Булево - Генерация счета юрлица, иначе физ лица
//
// Возвращаемое значение:
//  Строка - Номер счета
Функция НомерСчета(БИК, КодВалюты = "810", ЮрЛицо = Истина) Экспорт

	Если СтрДлина(КодВалюты) <> 3 Тогда
		ВызватьИсключение "Длина кода валюты должна быть равна 3";
	КонецЕсли;

	БалансовыйСчет = ?(ЮрЛицо, "40702", "40802");
	НомерСчетаВБанке = ЮТТестовыеДанные.СлучайнаяСтрока(11, , "0123456789");

	ЧислоДляРасчетаКонтрольнойСуммы = Прав(БИК, 3) + БалансовыйСчет + КодВалюты + "0" + НомерСчетаВБанке;
	Весы = ВесовыеКоэффициентаДляРасчетаКонтрольнойСуммыСчета();

	КонтрольнаяСумма = 0;
	Для Индекс = 0 По 22 Цикл
		КонтрольнаяСумма =
			КонтрольнаяСумма
			+ Число(Сред(ЧислоДляРасчетаКонтрольнойСуммы, Индекс + 1, 1)) * Число(Весы.Получить(Индекс))
		;
	КонецЦикла;

	КонтрольноеЧисло = ((КонтрольнаяСумма % 10) * 3) % 10;

	Возврат СтрШаблон(
		"%1%2%3%4",
		БалансовыйСчет,
		КодВалюты,
		КонтрольноеЧисло,
		НомерСчетаВБанке
	);

КонецФункции

// Формирует случайный валидный БИК банка
//
// Возвращаемое значение:
//  Строка - БИК
Функция БИК() Экспорт
	Возврат	СтрШаблон(
		"%1%2%3%4",
		"04", // код страны
		ЮТТестовыеДанные.СлучайнаяСтрока(2, , "0123456789"), // ОКАТО код территории
		ЮТТестовыеДанные.СлучайнаяСтрока(2, , "0123456789"), // код подразделение расчетной сети ЦБ
		Формат(ЮТТестовыеДанные.СлучайноеЧисло(50, 999), "ЧЦ=3; ЧВН=;") // номер кредитной организации
	);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяРеализации()
	Возврат "Банки";
КонецФункции

Функция ВесовыеКоэффициентаДляРасчетаКонтрольнойСуммыСчета()
	Возврат СтрРазделить("7,1,3,7,1,3,7,1,3,7,1,3,7,1,3,7,1,3,7,1,3,7,1", ",");
КонецФункции

#КонецОбласти
