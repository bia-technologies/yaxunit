//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция СкомпилироватьВнешнююОбработку(ТекстМодуляОбъект = "", Знач ТекстМодуляОбычнойФормы = "", Знач ТекстМодуляУправляемойФормы = "") Экспорт
	
	ИдентификаторМодуляОбъекта = "00ab4620-8498-4b18-a4f8-18b53138fcb5.0";
	ИдентификаторФормы = "a10cd1fd-c4ba-437c-9925-b7354192a1ea.0";
	
	Редактор = Обработки.ЮТРедакторОбработки.Создать();
	Редактор.ЗагрузитьШаблон();
	Редактор.УстановитьДанныеФайла(ИдентификаторМодуляОбъекта + "/text", ТекстМодуляОбъект);
	
	ДанныеФормы = Редактор.ТекстФайла(ИдентификаторФормы);
	ДанныеФормы = СтрЗаменить(ДанныеФормы, "MODULE_PLACEHOLDER", ТекстМодуляУправляемойФормы(ТекстМодуляУправляемойФормы));
	Редактор.УстановитьДанныеФайла(ИдентификаторФормы, ДанныеФормы);
	
	Возврат Редактор.СформироватьОбработку();
	
КонецФункции

Функция СкомпилироватьИПодключитьВнешнююОбработку(ТекстМодуляОбъект = "",
												  Знач ТекстМодуляОбычнойФормы = "",
												  Знач ТекстМодуляУправляемойФормы = "") Экспорт
	
	Данные = СкомпилироватьВнешнююОбработку(ТекстМодуляОбъект, ТекстМодуляОбычнойФормы, ТекстМодуляУправляемойФормы);
	
	Возврат ВнешниеОбработки.Подключить(ПоместитьВоВременноеХранилище(Данные));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстМодуляУправляемойФормы(ОсновнойТекст)
	
	Если НЕ ЗначениеЗаполнено(ОсновнойТекст) Тогда
		Возврат ОсновнойТекст;
	КонецЕсли;
	
	ОсновнойТекст = СтрЗаменить(ОсновнойТекст, """", """""");
	
	МассивСтрок = СтрРазделить(ОсновнойТекст, Символы.ПС);
	Директива = "&НаКлиенте ";
	
	Для Инд = 0 По МассивСтрок.ВГраница() Цикл
		Строка = МассивСтрок[Инд];
		Если СтрНачинаетсяС(СокрЛ(Строка), "Процедура ") Или СтрНачинаетсяС(СокрЛ(Строка), "Функция ") Тогда
			МассивСтрок[Инд] = Директива + Строка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(МассивСтрок, Символы.ПС);
	
КонецФункции

#КонецОбласти
