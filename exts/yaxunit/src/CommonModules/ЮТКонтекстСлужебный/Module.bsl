//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет начальную настройку для работы с хранимым контекстом
// 
// Параметры:
//  ПараметрыЗапуска - см. ЮТФабрика.ПараметрыЗапуска
Процедура ИнициализироватьКонтекст(ПараметрыЗапуска) Экспорт
	
#Если НЕ Клиент Тогда
	ВызватьИсключение "Метод `ИнициализироватьКонтекст` должен вызываться только с клиента";
#Иначе
	ЮТКонтекстСлужебныйКлиент.ИнициализироватьКонтекст(ПараметрыЗапуска);
	ЮТКонтекстСлужебныйВызовСервера.ИнициализироватьКонтекст(ПараметрыЗапуска);
	ОбновитьПовторноИспользуемыеЗначения();
#КонецЕсли
	
КонецПроцедуры

// Обработчик события "ИнициализацияКонтекста"
// 
// Параметры:
//  ДанныеКонтекста - Структура
Процедура ИнициализацияКонтекста(ДанныеКонтекста) Экспорт
	
	ДанныеКонтекста.Вставить(ИмяКонтекстаЭтапПрогона(), ЮТФабрика.ЭтапыПрогона().Инициализация);
	ДанныеКонтекста.Вставить(ИмяГлобальныеНастройкиВыполнения(), ДанныеКонтекста[ИмяКонтекстаПараметрыЗапуска()].settings);
	
КонецПроцедуры

// ДанныеКонтекста
//  Возвращает хранимые данные контекста.
//  Существует отдельно контекст сервера, отдельно клиента, эти контексты никак не связаны и никак не синхронизируются
// Возвращаемое значение:
//  Структура - Данные контекста
Функция ДанныеКонтекста() Экспорт
	
#Если Клиент Тогда
	Возврат ЮТКонтекстСлужебныйКлиент.ДанныеКонтекста();
#Иначе
	//@skip-check constructor-function-return-section
	Возврат ЮТКонтекстСлужебныйВызовСервера.ДанныеКонтекста();
#КонецЕсли
	
КонецФункции

// ЗначениеКонтекста
//  Возвращает значение вложенного контекста, вложенного реквизита контекста
// Параметры:
//  ИмяРеквизита - Строка - Имя реквизита/вложенного контекста
//  ПолучитьССервера - Булево - Получить значение из серверного контекста
// Возвращаемое значение:
//  - Структура - Значение реквизита/вложенного контекста
// 	- Неопределено
Функция ЗначениеКонтекста(ИмяРеквизита, ПолучитьССервера = Ложь) Экспорт
	
#Если Клиент Тогда
	Если ПолучитьССервера Тогда
		//@skip-check constructor-function-return-section
		Возврат ЮТКонтекстСлужебныйВызовСервера.ЗначениеКонтекста(ИмяРеквизита);
	КонецЕсли;
#КонецЕсли
	
	Объект = ДанныеКонтекста();
	
	Если Объект = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Ключи = СтрРазделить(ИмяРеквизита, ".");
	Для Инд = 0 По Ключи.Количество() - 2 Цикл
		Объект = Объект[Ключи[Инд]];
	КонецЦикла;
	
	//@skip-check constructor-function-return-section
	Возврат ЮТКоллекции.ЗначениеСтруктуры(Объект, Ключи[Ключи.ВГраница()]);
	
КонецФункции

// УстановитьЗначениеКонтекста
// Устанавливает значение вложенного контекста, вложенного реквизита контекста
// 
// Параметры:
//  ИмяРеквизита - Строка - Имя реквизита/вложенного контекста
//  Значение - Произвольный - Новое значение реквизита/вложенного контекста
//  УстановитьНаСервер - Булево - Установить также на сервер
Процедура УстановитьЗначениеКонтекста(Знач ИмяРеквизита, Знач Значение, Знач УстановитьНаСервер = Ложь) Экспорт
	
	ДанныеКонтекста = ДанныеКонтекста();
	
	Объект = ДанныеКонтекста;
	Ключи = СтрРазделить(ИмяРеквизита, ".");
	Для Инд = 0 По Ключи.Количество() - 2 Цикл
		Объект = Объект[Ключи[Инд]];
	КонецЦикла;
	
	Объект.Вставить(Ключи[Ключи.ВГраница()], Значение);
	
#Если НЕ Сервер Тогда
	Если УстановитьНаСервер Тогда
		ЮТКонтекстСлужебныйВызовСервера.УстановитьЗначениеКонтекста(ИмяРеквизита, Значение);
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

// УстановитьЗначениеКонтекста
// Устанавливает значение вложенного контекста, вложенного реквизита контекста
// 
// Параметры:
//  ИмяРеквизита - Строка - Имя реквизита/вложенного контекста
//  Значения - Структура, Соответствие из Произвольный - Набор устанавливаемых значений контекста
//  УстановитьНаСервер - Булево - Установить также на сервер
Процедура ВставитьЗначениеКонтекста(Знач ИмяРеквизита, Знач Значения, Знач УстановитьНаСервер = Ложь) Экспорт
	
#Если НЕ Сервер Тогда
	Если УстановитьНаСервер Тогда
		ЮТКонтекстСлужебныйВызовСервера.ВставитьЗначениеКонтекста(ИмяРеквизита, Значения);
		Возврат;
	КонецЕсли;
#КонецЕсли
	
	ДанныеКонтекста = ЗначениеКонтекста(ИмяРеквизита);
	
	Для Каждого Элемент Из Значения Цикл
		ДанныеКонтекста.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
КонецПроцедуры

// КонтекстТеста
//  Возвращает структуру, в которой можно хранить данные используемые в тесте
//  Данные живут в рамках одного теста, но доступны в обработчиках событий `ПередКаждымТестом` и `ПослеКаждогоТеста`
//  Например, в контекст можно помещать создаваемые данные, что бы освободить/удалить их в обработчике `ПослеКаждогоТеста`
// 
// Параметры:
//  ПолучитьССервера - Булево - Получить контекст с сервера
// 
// Возвращаемое значение:
//  - Структура - Контекст теста
//  - Неопределено - Если метод вызывается за рамками теста
Функция КонтекстТеста(ПолучитьССервера = Ложь) Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат ЗначениеКонтекста(ИмяКонтекстаТеста(), ПолучитьССервера);
	
КонецФункции

// КонтекстНабора
//  Возвращает структуру, в которой можно хранить данные используемые в тестах набора
//  Данные живут в рамках одного набора тестов (данные между клиентом и сервером не синхронизируются)
//  Доступны в каждом тесте набора и в обработчиках событий 
//  	+ `ПередТестовымНабором`
//  	+ `ПослеТестовогоНабора`
//  	+ `ПередКаждымТестом`
//  	+ `ПослеКаждогоТеста`
//  Например, в контекст можно помещать создаваемые данные, что бы освободить/удалить их в обработчике `ПослеКаждогоТеста`
// Возвращаемое значение:
//  - Структура - Контекст набора тестов
//  - Неопределено - Если метод вызывается за рамками тестового набора
Функция КонтекстНабора() Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат ЗначениеКонтекста(ИмяКонтекстаНабораТестов());
	
КонецФункции

// КонтекстМодуля
//  Возвращает структуру, в которой можно хранить данные используемые в тестах модуля
//  Данные живут в рамках одного тестового модуля (данные между клиентом и сервером не синхронизируются)
//  Доступны в каждом тесте модуля и в обработчиках событий 
//  Например, в контекст можно помещать создаваемые данные, что бы освободить/удалить их в обработчике `ПослеВсехТестов`
// Возвращаемое значение:
//  - Структура - Контекст тестового модуля
//  - Неопределено - Если метод вызывается за рамками тестового модуля
Функция КонтекстМодуля() Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат ЗначениеКонтекста(ИмяКонтекстаМодуля());
	
КонецФункции

Функция ГлобальныеНастройкиВыполнения() Экспорт
	
	Возврат ЗначениеКонтекста(ИмяГлобальныеНастройкиВыполнения());
	
КонецФункции

// КонтекстПроверки
//  Возвращает служебный контекста, данные выполняемой проверки
// Возвращаемое значение:
//  Неопределено, Структура - Контекст проверки
Функция КонтекстПроверки() Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат ЗначениеКонтекста(ИмяКонтекстаУтверждений());
	
КонецФункции

// КонтекстЧитателя
//  Возвращает служебный контекста, данные необходимые на этапе загрузки тестов
// Возвращаемое значение:
//  Неопределено, Структура - Контекст проверки
Функция КонтекстЧитателя() Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат ЗначениеКонтекста(ИмяКонтекстаЧитателя());
	
КонецФункции

// КонтекстЧитателя
//  Возвращает служебный контекста, данные используемые исполнителем тестов
// Возвращаемое значение:
//  см. ЮТФабрикаСлужебный.НовыйКонтекстИсполнения
Функция КонтекстИсполнения() Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат ЗначениеКонтекста(ИмяКонтекстаИсполнения());
	
КонецФункции

// Контекст исполнения текущего уровня.
// 
// Возвращаемое значение:
//  - Неопределено
//  - См. ЮТФабрика.ОписаниеИсполняемогоТестовогоМодуля
//  - См. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
//  - См. ЮТФабрика.ОписаниеИсполняемогоТеста
Функция КонтекстИсполненияТекущегоУровня() Экспорт
	
	Уровни = ЮТФабрика.УровниИсполнения();
	КонтекстИсполнения = КонтекстИсполнения();
	
	Если КонтекстИсполнения.Уровень = Уровни.Модуль Тогда
		
		Возврат КонтекстИсполнения.Модуль;
		
	ИначеЕсли КонтекстИсполнения.Уровень = Уровни.НаборТестов Тогда
		
		Возврат КонтекстИсполнения.Набор;
		
	ИначеЕсли КонтекстИсполнения.Уровень = Уровни.Тест Тогда
		
		Возврат КонтекстИсполнения.Тест;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция ТекущийЭтапПрогона() Экспорт
	
	Возврат ЗначениеКонтекста(ИмяКонтекстаЭтапПрогона());
	
КонецФункции

// см. ЮТФабрика.ПараметрыЗапуска
Функция ПараметрыЗапуска() Экспорт
	
	Возврат ЗначениеКонтекста(ИмяКонтекстаПараметрыЗапуска());
	
КонецФункции

Функция ОписаниеКонтекста(ПараметрыЗапуска) Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить(ИмяКонтекстаПараметрыЗапуска(), ПараметрыЗапуска);
	Описание.Вставить(ИмяКонтекстаЭтапПрогона(), ЮТФабрика.ЭтапыПрогона().Инициализация);
	
	ЮТСобытияСлужебный.ИнициализацияКонтекста(Описание);
	
	Возврат Описание;
	
КонецФункции

Процедура УстановитьКонтекстУтверждений(Знач ДанныеКонтекста) Экспорт
	
	УстановитьЗначениеКонтекста(ИмяКонтекстаУтверждений(), ДанныеКонтекста);
	
КонецПроцедуры

Процедура УстановитьКонтекстНабораТестов() Экспорт
	
	УстановитьЗначениеКонтекста(ИмяКонтекстаНабораТестов(), Новый Структура);
	
КонецПроцедуры

Процедура УстановитьКонтекстМодуля() Экспорт
	
	УстановитьЗначениеКонтекста(ИмяКонтекстаМодуля(), Новый Структура);
	
КонецПроцедуры

Процедура УстановитьКонтекстТеста() Экспорт
	
	УстановитьЗначениеКонтекста(ИмяКонтекстаТеста(), Новый Структура);
	
КонецПроцедуры

Процедура УстановитьКонтекстЧитателя(Знач ДанныеКонтекста) Экспорт
	
	УстановитьЗначениеКонтекста(ИмяКонтекстаЧитателя(), ДанныеКонтекста, Истина);
	
КонецПроцедуры

Процедура УстановитьГлобальныеНастройкиВыполнения(Знач Настройки) Экспорт
	
	УстановитьЗначениеКонтекста(ИмяГлобальныеНастройкиВыполнения(), Настройки, Истина);
	
КонецПроцедуры

Процедура УстановитьТекущийЭтапПрогона(Этап) Экспорт
	
	Если НЕ ЮТФабрика.ЭтапыПрогона().Свойство(Этап) Тогда
		ВызватьИсключение СтрШаблон("Передан неизвестный этап тестового прогона: `%1`", Этап);
	КонецЕсли;
	
	УстановитьЗначениеКонтекста(ИмяКонтекстаЭтапПрогона(), Этап, Истина);
	
КонецПроцедуры

Процедура УдалитьКонтекст() Экспорт
	
#Если Клиент Тогда
	ЮТКонтекстСлужебныйКлиент.УдалитьКонтекст();
#КонецЕсли
	ЮТКонтекстСлужебныйВызовСервера.УдалитьКонтекст();
	
КонецПроцедуры

Функция ЭтоЭтапТестовогоПрогона() Экспорт
	
	ТекущийЭтап = ТекущийЭтапПрогона();
	
	Если ТекущийЭтап = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ТекущийЭтап = ЮТФабрика.ЭтапыПрогона().ПрогонТестов;
	
КонецФункции

Функция ЭтоЭтапРегистрацииТестов() Экспорт
	
	ТекущийЭтап = ТекущийЭтапПрогона();
	
	Если ТекущийЭтап = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ТекущийЭтап = ЮТФабрика.ЭтапыПрогона().ЗагрузкаТестов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяКонтекстаУтверждений()
	
	Возврат "КонтекстУтверждения";
	
КонецФункции

Функция ИмяКонтекстаНабораТестов()
	
	Возврат "КонтекстНабора";
	
КонецФункции

Функция ИмяКонтекстаМодуля()
	
	Возврат "КонтекстМодуля";
	
КонецФункции

Функция ИмяКонтекстаТеста()
	
	Возврат "КонтекстТеста";
	
КонецФункции

Функция ИмяКонтекстаЧитателя()
	
	Возврат "КонтекстЧитателя";
	
КонецФункции

Функция ИмяГлобальныеНастройкиВыполнения()
	
	Возврат "ГлобальныеНастройкиВыполнения";
	
КонецФункции

Функция ИмяКонтекстаИсполнения() Экспорт
	
	Возврат "КонтекстИсполнения";
	
КонецФункции

Функция ИмяКонтекстаПараметрыЗапуска()
	
	Возврат "КонтекстПараметрыЗапуска";
	
КонецФункции

Функция ИмяКонтекстаЭтапПрогона()
	
	Возврат "КонтекстЭтапПрогона";
	
КонецФункции

#КонецОбласти
