//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

#Область Перечисления

// Типы ошибок.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Типы возможных ошибок:
//  * ТестНеРеализован - Строка
//  * ОшибкаСравнения - Строка
//  * ОшибкаОбработкиСобытия - Строка
//  * Утверждений - Строка
//  * Исполнения - Строка
//  * ЧтенияТестов - Строка
//  * МалоПараметров - Строка
//  * МногоПараметров - Строка
//  * НекорректныйКонтекстИсполнения - Строка
//  * Пропущен - Строка
Функция ТипыОшибок() Экспорт
	
	Типы = Новый Структура;
	
	Для Каждого ТипОшибки Из ПараметрыТиповОшибок() Цикл
		Типы.Вставить(ТипОшибки.Ключ, ТипОшибки.Ключ);
	КонецЦикла;
	
	//@skip-check constructor-function-return-section
	Возврат Новый ФиксированнаяСтруктура(Типы);
	
КонецФункции

Функция ПараметрыТиповОшибок(Кешировать = Истина) Экспорт
	
	Если Кешировать Тогда
		ЮТСлужебныйПовторногоИспользования.ПараметрыТиповОшибок();
	КонецЕсли;
	
	Статусы = ЮТФабрика.СтатусыИсполненияТеста();
	Реквизиты = "Ключ, Представление, Статус";
	
	ТипыОшибок = Новый Массив();
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "ТестНеРеализован", "Не реализован", Статусы.НеРеализован));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "ОшибкаСравнения", "Ошибка сравнения", Статусы.Ошибка));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "ОшибкаОбработкиСобытия", "Ошибка обработки события", Статусы.Сломан));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "Утверждений", "Ошибка утверждений", Статусы.Ошибка));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "Исполнения", "Ошибка исполнения", Статусы.Сломан));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "ЧтенияТестов", "Загрузки тестов", Статусы.Сломан));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "МалоПараметров", "Мало фактических параметров", Статусы.Сломан));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "МногоПараметров", "Много фактических параметров", Статусы.Сломан));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "НекорректныйКонтекстИсполнения", "Некорректный контекст исполнения", Статусы.Пропущен));
	ТипыОшибок.Добавить(Новый Структура(Реквизиты, "Пропущен", "Пропущен", Статусы.Пропущен));
	
	Возврат ЮТКоллекции.ВСтруктуру(ТипыОшибок, "Ключ");
	
КонецФункции

#КонецОбласти

#Область СтруктурыДанных

// Описание тестового модуля.
//
// Параметры:
//  МетаданныеМодуля - см. ОписаниеМетаданныеМодуля
//  НаборыТестов - Массив из см. ЮТФабрикаСлужебный.ОписаниеТестовогоНабора
//
// Возвращаемое значение:
//  Структура - Описание тестового модуля:
// * Метаданные - см. ОписаниеМетаданныеМодуля
// * Теги - Массив из Строка - Коллекция тегов набора
// * НаборыТестов - Массив из см. ЮТФабрикаСлужебный.ОписаниеТестовогоНабора
// * Ошибки - Массив из см. ЮТФабрикаСлужебный.ОписаниеВозникшейОшибки
// * НастройкиВыполнения- Структура - Настройки исполнения теста
// * Зависимости- Массив Из см. ЮТФабрика.НовоеОписаниеЗависимости - Зависимости тестового модуля
Функция ОписаниеТестовогоМодуля(МетаданныеМодуля, НаборыТестов) Экспорт
	
	Описание = БазовоеОписаниеРегистрируемогоОбъекта();
	
	Описание.Вставить("Метаданные", МетаданныеМодуля);
	Описание.Вставить("НаборыТестов", НаборыТестов);
	
	Возврат Описание;
	
КонецФункции

// ОписаниеТестовогоНабора
//  Возвращает описание регистрируемого тестового набора.
//  Эта структура используется на этапе формирования описаний имеющихся тестов
// Параметры:
//  Имя - Строка - Имя набора
//  ТегиСтрокой - Строка - Теги набора
//
// Возвращаемое значение:
//  Структура - Описание тестового набора:
// * Имя - Строка - Имя набора
// * Представление - Строка - Представление, краткое описание
// * Теги - Массив из Строка - Коллекция тегов набора
// * Тесты - Массив из см. ОписаниеТеста - Коллекция тестов набора, см. ОписаниеТеста
// * Ошибки - Массив из см. ЮТФабрикаСлужебный.ОписаниеВозникшейОшибки - Описания ошибок регистрации тестов
// * НастройкиВыполнения- Структура - Настройки исполнения теста
// * Зависимости- Массив Из см. ЮТФабрика.НовоеОписаниеЗависимости - Зависимости тестового набора
Функция ОписаниеТестовогоНабора(Имя, ТегиСтрокой = "") Экспорт
	
	Описание = БазовоеОписаниеРегистрируемогоОбъекта();
	
	Описание.Вставить("Имя", Строка(Имя));
	Описание.Вставить("Представление", Строка(Имя));
	Описание.Вставить("Тесты", Новый Массив);
	
	Если ЗначениеЗаполнено(ТегиСтрокой) Тогда
		Описание.Теги = ЮТСтроки.РазделитьСтроку(ТегиСтрокой, ",");
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

// ОписаниеТеста
//  Возвращает описание регистрируемого теста.
//  Эта структура используется на этапе формирования описаний имеющихся тестов
// Параметры:
//  Имя - Строка - Имя тестового метода
//  Представление - Строка - Представление, краткое описание теста
//  КонтекстыВызова - Массив из Строка - Контексты исполнения теста, см. ЮТФабрика.КонтекстыВызова
//  Теги - Строка - Строка тегов разделенных запятыми
//       - Массив из Строка - Набор тегов
//
// Возвращаемое значение:
//  Структура - Описание теста:
// * Имя - Строка - Имя теста (тестового метода)
// * Представление - Строка - Представление теста
// * Теги - Массив из Строка - Теги теста
// * КонтекстВызова - Массив из Строка - Контексты исполнения теста, см. ЮТФабрика.КонтекстыВызова
// * НастройкиВыполнения- Структура - Настройки исполнения теста
// * Параметры - Неопределено, Массив из Произвольный - Параметры теста
// * НомерВНаборе - Число - Порядковый номер теста в наборе
// * Зависимости- Массив Из см. ЮТФабрика.НовоеОписаниеЗависимости - Зависимости теста
// * Ошибки - Массив из см. ЮТФабрикаСлужебный.ОписаниеВозникшейОшибки - Описания ошибок регистрации тестов
Функция ОписаниеТеста(Имя, Представление, КонтекстыВызова, Знач Теги = "") Экспорт
	
	Описание = БазовоеОписаниеРегистрируемогоОбъекта();
	Описание.Вставить("Имя", Строка(Имя));
	Описание.Вставить("Представление", Строка(Представление));
	Описание.Вставить("КонтекстВызова", КонтекстыВызова);
	Описание.Вставить("Параметры", Неопределено);
	Описание.Вставить("НомерВНаборе", 0);
	
	Если ТипЗнч(Теги) = Тип("Строка") Тогда
		Описание.Теги = ЮТСтроки.РазделитьСтроку(Теги, ",");
	ИначеЕсли ЗначениеЗаполнено(Теги) Тогда
		Описание.Теги = Теги;
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

// Создает описание исполняемого тестового модуля.
//  Содержит всю необходимую информацию для прогона тестов, а также данные результата
// Параметры:
//  ТестовыйМодуль - см. ОписаниеТестовогоМодуля
//
// Возвращаемое значение:
//  см. ЮТФабрика.ОписаниеИсполняемогоТестовогоМодуля
Функция НовоеОписаниеИсполняемогоТестовогоМодуля(ТестовыйМодуль) Экспорт
	
	Описание = ЮТФабрика.ОписаниеИсполняемогоТестовогоМодуля();
	ЗаполнитьБазовоеОписаниеИсполняемогоОбъекта(Описание, ТестовыйМодуль);
	
	Описание.Метаданные = ТестовыйМодуль.Метаданные;
	Описание.НаборыТестов = Новый Массив;
	
	Возврат Описание;
	
КонецФункции

// Создает описание исполняемого тестового набора
//  Возвращает описание исполняемого тестового набора.
//  Содержит данные необходимые для выполнения прогона тестов
// Параметры:
//  НаборТестов - См. ОписаниеТестовогоНабора
//
// Возвращаемое значение:
//  см. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
Функция НовоеОписаниеИсполняемогоНабораТестов(НаборТестов) Экспорт
	
	Описание = ЮТФабрика.ОписаниеИсполняемогоНабораТестов();
	ЗаполнитьБазовоеОписаниеИсполняемогоОбъекта(Описание, НаборТестов);
	
	Описание.Имя = НаборТестов.Имя;
	Описание.Представление = НаборТестов.Представление;
	Описание.Режим = "";
	Описание.Тесты = Новый Массив;
	Описание.Выполнять = Описание.Ошибки.Количество() = 0;
	Описание.ДатаСтарта = 0;
	Описание.Длительность = 0;
	
	Возврат Описание;
	
КонецФункции

// ОписаниеИсполняемогоТеста
//  Возвращает описание исполняемого теста
//  Содержит данные необходимые для выполнения прогона тестов
//
// Параметры:
//  Тест - См. ОписаниеТеста
//  Режим - См. ЮТФабрика.КонтекстыВызова
//  ТестовыйМодуль - См. ОписаниеТестовогоМодуля
//
// Возвращаемое значение:
//  см. ЮТФабрика.ОписаниеИсполняемогоТеста
Функция НовоеОписаниеИсполняемогоТеста(Тест, Режим, ТестовыйМодуль) Экспорт
	
	ПолноеИмяМетода = СтрШаблон("%1.%2", ТестовыйМодуль.Метаданные.Имя, Тест.Имя);
	Представление = ПредставлениеТеста(Тест);
	
	Описание = ЮТФабрика.ОписаниеИсполняемогоТеста();
	ЗаполнитьБазовоеОписаниеИсполняемогоОбъекта(Описание, Тест);
	
	Описание.Имя = Представление;
	Описание.Метод = Тест.Имя;
	Описание.ПолноеИмяМетода = ПолноеИмяМетода;
	Описание.Режим = Режим;
	Описание.ДатаСтарта = 0;
	Описание.Длительность = 0;
	Описание.Статус = ЮТФабрика.СтатусыИсполненияТеста().Ожидание;
	Описание.Параметры = Тест.Параметры;
	Описание.НомерВНаборе = Тест.НомерВНаборе;
	
	//@skip-check constructor-function-return-section
	Возврат Описание;
	
КонецФункции

// ОписаниеВозникшейОшибки
//  Возвращает базовую структуру ошибки
//
// Параметры:
//  Сообщение - Строка - Описание ошибки
//
// Возвращаемое значение:
//  Структура - Описание возникшей ошибки:
// * Сообщение - Строка - Описание возникшей ошибки
// * Стек - Строка - Стек возникшей ошибки
// * ТипОшибки - Строка - Тип возникшей ошибки. Доступные значения см. ЮТФабрикаСлужебный.ТипыОшибок
// * Лог - Массив из Строка
Функция ОписаниеВозникшейОшибки(Сообщение) Экспорт
	
	Описание = Новый Структура("Стек, ТипОшибки", "", "");
	Описание.Вставить("Сообщение", Сообщение);
	Описание.Вставить("Лог", Новый Массив);
	
	Возврат Описание;
	
КонецФункции

// Возвращает базовую структуру ошибки проверки факта и ожидания
//
// Параметры:
//  Сообщение - Строка
//
// Возвращаемое значение:
//  Структура - Описание возникшей ошибки:
// * Сообщение - Строка - Описание возникшей ошибки
// * Стек - Строка - Стек возникшей ошибки
// * ТипОшибки - Строка - Тип возникшей ошибки. Доступные значения см. ЮТФабрикаСлужебный.ТипыОшибок
// * Лог - Массив из Строка
// * ПроверяемоеЗначение - Произвольный - Проверяемое, фактическое значение
// * ОжидаемоеЗначение - Произвольный - Ожидаемое значение
Функция ОписаниеОшибкиСравнения(Сообщение) Экспорт
	
	Описание = ОписаниеВозникшейОшибки(Сообщение);
	Описание.ТипОшибки = ТипыОшибок().Утверждений;
	Описание.Вставить("ПроверяемоеЗначение");
	Описание.Вставить("ОжидаемоеЗначение");
	
	//@skip-check constructor-function-return-section
	Возврат Описание;
	
КонецФункции

// Возвращает базовую структуру ошибки пропуска теста
//
// Параметры:
//  Сообщение - Строка
//
// Возвращаемое значение:
//  см. ОписаниеВозникшейОшибки
Функция ОписаниеОшибкиПропуска(Сообщение) Экспорт
	
	Описание = ОписаниеВозникшейОшибки(Сообщение);
	Описание.ТипОшибки = ТипыОшибок().Пропущен;
	
	Возврат Описание;
	
КонецФункции

// Описание события исполнения тестов.
//
// Параметры:
//  Модуль - см. ЮТФабрика.ОписаниеИсполняемогоТестовогоМодуля
//  Набор 	- см. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
//  Тест 	- см. ЮТФабрика.ОписаниеИсполняемогоТеста
//
// Возвращаемое значение:
//  Структура - Описание события исполнения тестов:
// * Модуль - см. ЮТФабрика.ОписаниеИсполняемогоТестовогоМодуля
// * Набор 	- см. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
// * Тест 	- см. ЮТФабрика.ОписаниеИсполняемогоТеста
Функция ОписаниеСобытияИсполненияТестов(Модуль, Набор = Неопределено, Тест = Неопределено) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("Модуль", Модуль);
	Описание.Вставить("Набор", Набор);
	Описание.Вставить("Тест", Тест);
	
	//@skip-check constructor-function-return-section
	Возврат Описание;
	
КонецФункции

// Формирует описание проверяемого значения.
// Используется в утверждения для формирования дружелюбного сообщения об ошибке.
//
// Параметры:
//  ПроверяемоеЗначение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//  Структура - Описание проверяемого значения:
// * Значение - Произвольный
// * Представление - Строка - Представление объекта
//                 - Неопределено - Если не указано тогда используется платформенное представление (`Строка(Значение)`)
// * ИмяСвойства - Строка, Число, Неопределено - Имя проверяемого реквизита, индекса
Функция ОписаниеПроверяемогоЗначения(ПроверяемоеЗначение) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("Значение", ПроверяемоеЗначение);
	Описание.Вставить("Представление", Неопределено);
	Описание.Вставить("ИмяСвойства", Неопределено);
	
	Возврат Описание;
	
КонецФункции

// Описание проверки.
//
// Параметры:
//  ПроверяемоеЗначение - Произвольный
//
// Возвращаемое значение:
//  Структура - Описание проверки:
// * ОбъектПроверки - см. ОписаниеПроверяемогоЗначения
// * ПрефиксОшибки - Строка, Неопределено -
// * ОписаниеПроверки - Строка, Неопределено -
Функция ОписаниеПроверки(ПроверяемоеЗначение) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("ОбъектПроверки", ОписаниеПроверяемогоЗначения(ПроверяемоеЗначение));
	Описание.Вставить("ПрефиксОшибки", Неопределено);
	Описание.Вставить("ОписаниеПроверки", Неопределено);
	
	Возврат Описание;
	
КонецФункции

#Область Зависимости

Функция РезультатРазрешенияЗависимости() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Успешно", Ложь);
	Результат.Вставить("Ошибка", Неопределено);
	Результат.Вставить("СохраняемыйКонтекст", Неопределено);
	Результат.Вставить("СохраняемыйКонтекстКлиент", Неопределено);
	Результат.Вставить("СохраняемыйКонтекстСервер", Неопределено);
	Результат.Вставить("ДоступностьНаКлиенте", Истина);
	Результат.Вставить("ДоступностьНаСервере", Истина);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область КонструкторыКонтекстов

// Данные контекста исполнения.
//
// Возвращаемое значение:
//  Структура - Данные контекста исполнения:
//  * Уровень - Строка - Возможные значения см. ЮТФабрика.УровниИсполнения
//  * Модуль  - Неопределено - Нет исполняемого модуля
//            - см. ЮТФабрика.ОписаниеИсполняемогоТестовогоМодуля
//  * Набор   - Неопределено - Нет исполняемого тестового набора
//            - см. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
//  * Тест    - Неопределено - Нет исполняемого теста
//            - см. ЮТФабрика.ОписаниеИсполняемогоТеста
Функция НовыйКонтекстИсполнения() Экспорт
	
	Контекст = Новый Структура();
	Контекст.Вставить("Уровень", "");
	Контекст.Вставить("Модуль", Неопределено);
	Контекст.Вставить("Набор", Неопределено);
	Контекст.Вставить("Тест", Неопределено);
	
	Возврат Контекст;
	
КонецФункции

// Описание результата проверки.
//
// Параметры:
//  Успешно - Булево - Успешно
//
// Возвращаемое значение:
//  Структура - Описание результата проверки:
// * Сообщения - Массив из Произвольный
// * Успешно - Булево
Функция ОписаниеРезультатаПроверки(Успешно = Истина) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("Сообщения", Новый Массив);
	Описание.Вставить("Успешно", Успешно);
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

// КонтекстыМодуля
//  Возвращает коллекцию доступных контекстов выполнения модуля
// Параметры:
//  Модуль - См. ОписаниеМетаданныеМодуля
//
// Возвращаемое значение:
//  Массив из Строка - Контексты модуля, возможные значения см. ЮТФабрика.КонтекстыВызова
Функция КонтекстыМодуля(Модуль) Экспорт
	
	Контексты = ЮТФабрика.КонтекстыВызова();
	
	КонтекстыМодуля = Новый Массив();
	
	Если Модуль.Сервер Тогда
		КонтекстыМодуля.Добавить(Контексты.Сервер);
	КонецЕсли;
	
	Если Модуль.КлиентУправляемоеПриложение Тогда
		КонтекстыМодуля.Добавить(Контексты.КлиентУправляемоеПриложение);
	КонецЕсли;
	
	Если Модуль.КлиентОбычноеПриложение Тогда
		КонтекстыМодуля.Добавить(Контексты.КлиентОбычноеПриложение);
	КонецЕсли;
	
	Возврат КонтекстыМодуля;
	
КонецФункции

// КонтекстыПриложения
//  Возвращает коллекцию доступных контекстов приложения
// Возвращаемое значение:
//  Массив из Строка - Контексты приложения, возможные значения см. ЮТФабрика.КонтекстыВызова
Функция КонтекстыПриложения() Экспорт
	
	Окружение = ЮТОкружение.ОписаниеОкружения();
	Контексты = ЮТФабрика.КонтекстыВызова();
	КонтекстыПриложения = Новый Массив();
	
	КонтекстыПриложения.Добавить(Контексты.Сервер);
	КонтекстыПриложения.Добавить(Контексты.ВызовСервера);
	
	Если Окружение.ОбычноеПриложение Тогда
		КонтекстыПриложения.Добавить(Контексты.КлиентОбычноеПриложение);
	Иначе
		КонтекстыПриложения.Добавить(Контексты.КлиентУправляемоеПриложение);
	КонецЕсли;
	
	Возврат КонтекстыПриложения;
	
КонецФункции

// КонтекстИсполнения
//  Возвращает контекст исполнения по контексту вызова
// Параметры:
//  КонтекстВызова - Строка - Контекст вызова, см. ЮТФабрика.КонтекстыВызова
//
// Возвращаемое значение:
//  Неопределено, Строка - Контекст исполнения
Функция КонтекстИсполнения(КонтекстВызова) Экспорт
	
	КонтекстыВызова = ЮТФабрика.КонтекстыВызова();
	
	Если КонтекстВызова = КонтекстыВызова.Сервер Тогда
		
		Возврат ЮТФабрика.КонтекстыИсполнения().Сервер;
		
	ИначеЕсли КонтекстВызова = КонтекстыВызова.КлиентУправляемоеПриложение
		ИЛИ КонтекстВызова = КонтекстыВызова.КлиентОбычноеПриложение
		ИЛИ КонтекстВызова = КонтекстыВызова.ВызовСервера Тогда
		
		Возврат ЮТФабрика.КонтекстыИсполнения().Клиент;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Формирует представление теста
//
// Параметры:
//  Тест - см. ОписаниеТеста
//
// Возвращаемое значение:
//  Строка - Представление теста
Функция ПредставлениеТеста(Тест) Экспорт
	
	Если ЗначениеЗаполнено(Тест.Представление) Тогда
		Представление = Тест.Представление;
	ИначеЕсли ЗначениеЗаполнено(Тест.Параметры) Тогда
		ПредставлениеПараметров = СтрСоединить(Тест.Параметры, ", ");
		Представление = СтрШаблон("%1(%2)", Тест.Имя, ПредставлениеПараметров);
	Иначе
		Представление = Тест.Имя;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ПараметрыЗаполненияТаблицыЗначений(Знач ПараметрыСозданияОбъектов = Неопределено) Экспорт
	
	Если ПараметрыСозданияОбъектов = Неопределено Тогда
		ПараметрыСозданияОбъектов = ЮТФабрика.ПараметрыСозданияОбъектов();
	Иначе
		ВходныеПараметрыСозданияОбъектов = ПараметрыСозданияОбъектов;
		ПараметрыСозданияОбъектов = ЮТФабрика.ПараметрыСозданияОбъектов();
		ЗаполнитьЗначенияСвойств(ПараметрыСозданияОбъектов, ВходныеПараметрыСозданияОбъектов);
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура("СозданиеОбъектовМетаданных", ПараметрыСозданияОбъектов);
	
КонецФункции

// Параметры генератора отчета.
// Описывает предоставляемые модулем форматы отчетов
//
// Возвращаемое значение:
//  Структура - Параметры генератора отчета:
// * Форматы - Структура - Форматы отчетов, предоставляемые модулем
Функция ПараметрыГенератораОтчета() Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("Форматы", Новый Структура);
	
	Возврат Параметры;
	
КонецФункции

// Описание формата отчета.
//
// Параметры:
//  Идентификатор - Строка - Уникальный идентификатор формата отчета
//  Представление - Строка - Пользовательское представление отчета, выводится в форму настроек тестирования
//
// Возвращаемое значение:
//  Структура - Описание формата отчета:
// * Идентификатор - Строка - Уникальный идентификатор формата отчета
// * Представление - Строка - Пользовательское представление отчета, выводится в форму настроек тестирования
// * ЗаписьВКаталог - Булево - Отчет записывается в каталог, в этом случае должен быть установлен признак `СамостоятельнаяЗаписьОтчета`
// * ФильтрВыбораФайла - Строка - Фильтр выбора, используется в форме настроек тестирования
// * ИмяФайлаПоУмолчанию - Строка - Для отчетов, записываемых в файл, имя файла если в параметрах указан каталог
// * СамостоятельнаяЗаписьОтчета - Булево - Способ записи отчета в файлы.
//											Истина - Модуль формирования отчета самостоятельно записывает отчет
//											Ложь - Модуль формирования отчета генерирует данные, которые записываются движком
Функция ОписаниеФорматаОтчета(Идентификатор, Представление) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("Идентификатор", Идентификатор);
	Описание.Вставить("Представление", Представление);
	Описание.Вставить("ЗаписьВКаталог", Ложь);
	Описание.Вставить("ФильтрВыбораФайла", "");
	Описание.Вставить("ИмяФайлаПоУмолчанию", "");
	Описание.Вставить("СамостоятельнаяЗаписьОтчета", Ложь);
	
	Возврат Описание;
	
КонецФункции

// Доступные свойства Allure
// 
// Возвращаемое значение:
//  Структура - Свойства:
// * Идентификатор - Строка
// * Описание - Строка
// * ОписаниеHTML - Строка
// * Серьезность - Строка
// * Владелец - Строка
// * Эпик - Строка
// * Функциональность - Строка
// * История - Строка
// * РодительскийНабор - Строка
// * Набор - Строка
// * ПодчиненныйНабор - Строка
// * Пакет - Строка
Функция СвойстваAllure() Экспорт
	
	Свойства = Новый Структура;
	
	Свойства.Вставить("Идентификатор", "testCaseId");
	Свойства.Вставить("Описание", "description");
	Свойства.Вставить("ОписаниеHTML", "descriptionHtml");
	Свойства.Вставить("Серьезность", "severity");
	Свойства.Вставить("Владелец", "owner");
	Свойства.Вставить("Эпик", "epic");
	Свойства.Вставить("Функциональность", "feature");
	Свойства.Вставить("История", "story");
	Свойства.Вставить("РодительскийНабор", "parentSuite");
	Свойства.Вставить("Набор", "suite");
	Свойства.Вставить("ПодчиненныйНабор", "subSuite");
	Свойства.Вставить("Пакет", "package");
	
	Возврат Свойства;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция БазовоеОписаниеРегистрируемогоОбъекта()
	
	Описание = Новый Структура();
	
	Описание.Вставить("Теги", Новый Массив);
	
	Описание.Вставить("НастройкиВыполнения", Новый Структура());
	Описание.Вставить("Зависимости", Новый Массив());
	
	Описание.Вставить("Ошибки", Новый Массив);
	
	Возврат Описание;
	
КонецФункции

Процедура ЗаполнитьБазовоеОписаниеИсполняемогоОбъекта(ИсполняемыйОбъект, РегистрируемыйОбъект)
	
	ИсполняемыйОбъект.Теги = ЮТКоллекции.СкопироватьМассив(РегистрируемыйОбъект.Теги);
	
	ИсполняемыйОбъект.НастройкиВыполнения = ЮТКоллекции.СкопироватьСтруктуру(РегистрируемыйОбъект.НастройкиВыполнения);
	ИсполняемыйОбъект.Зависимости = ЮТКоллекции.СкопироватьМассив(РегистрируемыйОбъект.Зависимости);
	
	ИсполняемыйОбъект.Ошибки = ЮТКоллекции.СкопироватьМассив(РегистрируемыйОбъект.Ошибки);
	
	Если РегистрируемыйОбъект.Свойство("СвойстваОтчета") Тогда
		ИсполняемыйОбъект.Вставить("СвойстваОтчета", ЮТКоллекции.СкопироватьСоответствие(РегистрируемыйОбъект.СвойстваОтчета));
	КонецЕсли;
	Если РегистрируемыйОбъект.Свойство("СвойстваAllure") Тогда
		ИсполняемыйОбъект.Вставить("СвойстваAllure", ЮТКоллекции.СкопироватьСоответствие(РегистрируемыйОбъект.СвойстваAllure));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
