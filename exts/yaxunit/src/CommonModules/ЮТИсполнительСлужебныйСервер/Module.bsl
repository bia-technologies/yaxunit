//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2024 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция СерверноеХранилищеТестов() Экспорт
	
	АдресХранилища = ЮТКонтекстСлужебный.ЗначениеКонтекста(ИмяКонтекстаХранилище());
	
	Если АдресХранилища = Неопределено Тогда
		ВызватьИсключение "Не удалось получить адрес серверного хранилища тестов.
						  |Возможно сломана транзакция. ТранзакцияАктивна=" + ТранзакцияАктивна();
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресХранилища);
	
КонецФункции

Процедура СохранитьИнформациюОТестовыхСценариях(ТестовыеМодули) Экспорт
	
	Хранилище = Новый Соответствие();
	
	Для Каждого ОсновнойМодуль Из ТестовыеМодули Цикл
		
		Модуль = ЮТКоллекции.СкопироватьСтруктуру(ОсновнойМодуль, Ложь);
		Идентификатор = ЮТИсполнительСлужебныйКлиентСервер.ИдентификаторТестовогоМодуля(Модуль);
		
		Хранилище.Вставить(Идентификатор, Модуль);
		
		Для Каждого Набор Из ОсновнойМодуль.НаборыТестов Цикл
			
			Идентификатор = ЮТИсполнительСлужебныйКлиентСервер.ИдентификаторТестовогоНабора(Модуль, Набор);
			Хранилище.Вставить(Идентификатор, Набор);
			
			Для Каждого Тест Из Набор.Тесты Цикл
				
				Идентификатор = ЮТИсполнительСлужебныйКлиентСервер.ИдентификаторТеста(Модуль, Набор, Тест);
				Хранилище.Вставить(Идентификатор, Тест);
				
			КонецЦикла;
			
		КонецЦикла;
		
		Модуль.НаборыТестов = Новый Массив; // Уже не нужно, поэтому очистим
		
	КонецЦикла;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Хранилище, Новый УникальныйИдентификатор());
	ЮТКонтекстСлужебный.УстановитьЗначениеКонтекста(ИмяКонтекстаХранилище(), АдресХранилища, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяКонтекстаХранилище()
	
	Возврат "ХранилищеТестовыхСценариев";
	
КонецФункции

#КонецОбласти
