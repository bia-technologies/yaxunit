//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2022 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

/////////////////////////////////////////////////////////////////////////////////
// Экспортные процедуры и функции, предназначенные для использования другими 
// объектами конфигурации или другими программами
///////////////////////////////////////////////////////////////////////////////// 
#Область ПрограммныйИнтерфейс

// КонтекстыВызова
//  Возвращает перечисление возможных контекстов вызова
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Контексты вызова
Функция КонтекстыВызова() Экспорт
	
	Контексты = Новый Структура();
	
	Контексты.Вставить("КлиентОбычноеПриложение", "КлиентОбычноеПриложение");
	Контексты.Вставить("КлиентУправляемоеПриложение", "КлиентУправляемоеПриложение");
	Контексты.Вставить("Сервер", "Сервер");
	Контексты.Вставить("ВызовСервера", "ВызовСервера");
	
	// TODO Подозреваю нужно добавить web клиент
	
	Возврат Новый ФиксированнаяСтруктура(Контексты);
	
КонецФункции

// Доступные параметры (настройки) исполнения тестов
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Параметры исполнения теста:
// * ВТранзакции - Строка - Тест должен выполняться в транзакции
Функция ПараметрыИсполненияТеста() Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("ВТранзакции", "ВТранзакции");
	
	Возврат Новый ФиксированнаяСтруктура(Параметры);
	
КонецФункции

#КонецОбласти

/////////////////////////////////////////////////////////////////////////////////
// Экспортные процедуры и функции для служебного использования внутри подсистемы
///////////////////////////////////////////////////////////////////////////////// 
#Область СлужебныйПрограммныйИнтерфейс

#Область Перечисления

// КонтекстыИсполнения
//  Возвращает перечисление возможных контекстов исполнения тестов
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Контексты исполнения
Функция КонтекстыИсполнения() Экспорт
	
	Контексты = Новый Структура();
	
	Контексты.Вставить("Клиент", "Клиент");
	Контексты.Вставить("Сервер", "Сервер");
	
	Возврат Новый ФиксированнаяСтруктура(Контексты);
	
КонецФункции

// СтатусыИсполненияТеста
//  Возвращает перечисление возможных статусов выполнения теста, жизненный цикл теста
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Статусы исполнения теста
Функция СтатусыИсполненияТеста() Экспорт
	
	Статусы = Новый Структура();
	
	Статусы.Вставить("Ожидание", "Ожидание");
	Статусы.Вставить("Исполнение", "Исполнение");
	Статусы.Вставить("Успешно", "Успешно");
	Статусы.Вставить("Ошибка", "Ошибка");
	Статусы.Вставить("Сломан", "Сломан");
	Статусы.Вставить("Пропущен", "Пропущен");
	Статусы.Вставить("НеРеализован", "НеРеализован");
	
	Возврат Новый ФиксированнаяСтруктура(Статусы);
	
КонецФункции

// Типы ошибок.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура
//  * ТестНеРеализован - Строка
//  * ОшибкаСравнения - Строка
//  * ОшибкаОбработкиСобытия - Строка
//  * Утверждений - Строка
//  * Исполнения - Строка
//  * ЧтенияТестов - Строка
//  * МалоПараметров - Строка
//  * МногоПараметров - Строка
//  * НекорректныйКонтекстИсполнения - Строка
//  * Пропущен - Строка
Функция ТипыОшибок() Экспорт
	
	Типы = Новый Структура;
	
	Для Каждого ТипОшибки Из ПараметрыТиповОшибок() Цикл
		Типы.Вставить(ТипОшибки.Ключ, ТипОшибки.Ключ);
	КонецЦикла;
	
	Возврат Новый ФиксированнаяСтруктура(Типы);
	
КонецФункции

Функция ПараметрыТиповОшибок(Кешировать = Истина) Экспорт
	
	Если Кешировать Тогда
		ЮТПовторногоИспользования.ПараметрыТиповОшибок();
	КонецЕсли;
	
	Возврат ЮТТестовыеДанные.СтруктураMarkDown("Ключ",
	"| Ключ                           | Представление                    | Статус       |
	||--------------------------------|----------------------------------|--------------|
	|| ТестНеРеализован               | Не реализован                    | НеРеализован |
	|| ОшибкаСравнения                | Ошибка сравнения                 | Ошибка       |
	|| ОшибкаОбработкиСобытия         | Ошибка обработки события         | Сломан       |
	|| Утверждений                    | Ошибка утверждений               | Ошибка       |
	|| Исполнения                     | Ошибка исполнения                | Сломан       |
	|| ЧтенияТестов                   | Загрузки тестов                  | Сломан       |
	|| МалоПараметров                 | Мало фактических параметров      | Сломан       |
	|| МногоПараметров                | Много фактических параметров     | Сломан       |
	|| НекорректныйКонтекстИсполнения | Некорректный контекст исполнения | Пропущен     |
	|| Пропущен                       | Пропущен                         | Пропущен     |
	|");
	
КонецФункции

Функция УровниИсполнения() Экспорт
	
	Уровни = Новый Структура;
	Уровни.Вставить("Модуль", "Модуль");
	Уровни.Вставить("НаборТестов", "НаборТестов");
	Уровни.Вставить("Тест", "Тест");
	
	Возврат Новый ФиксированнаяСтруктура(Уровни);
	
КонецФункции

#КонецОбласти

#Область СтруктурыДанных

// Описание тестового модуля.
// 
// Параметры:
//  МетаданныеМодуля - Структура из см. ЮТФабрика.ОписаниеМодуля
//  НаборыТестов - Массив из см. ЮТФабрика.ОписаниеТестовогоНабора
// 
// Возвращаемое значение:
//  Структура - Описание тестового модуля:
// * МетаданныеМодуля - Структура из см. ЮТФабрика.ОписаниеМодуля
// * НаборыТестов - Массив из см. ЮТФабрика.ОписаниеТестовогоНабора
// * Ошибки - Массив из см. ЮТФабрика.ОписаниеВозникшейОшибки 
Функция ОписаниеТестовогоМодуля(МетаданныеМодуля, НаборыТестов) Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("МетаданныеМодуля", МетаданныеМодуля);
	Описание.Вставить("НаборыТестов", НаборыТестов);
	Описание.Вставить("Ошибки", Новый Массив);
	
	Возврат Описание;
	
КонецФункции

// ОписаниеТестовогоНабора
//  Возвращает описание регистрируемого тестового набора.
//  Эта структура используется на этапе формирования описаний имеющихся тестов
// Параметры:
//  Имя - Строка - Имя набора
//  ТегиСтрокой - Строка - Теги набора
// 
// Возвращаемое значение:
//  Структура - Описание тестового набора:
// * Имя - Строка - Имя набора
// * Представление - Строка - Представление, краткое описание
// * Теги - Массив из Строка - Коллекция тегов набора
// * Тесты - Массив из см. ОписаниеТеста - Коллекция тестов набора, см. ОписаниеТеста
// * Ошибки - Массив из см. ЮТФабрика.ОписаниеВозникшейОшибки - Описания ошибок регистрации тестов
// * НастройкиВыполнения- Структура - Настройки исполнения теста
Функция ОписаниеТестовогоНабора(Имя, ТегиСтрокой = "") Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("Имя", Строка(Имя));
	Описание.Вставить("Представление", Строка(Имя));
	Описание.Вставить("Теги", СтрРазделить(ТегиСтрокой, ", ", Ложь));
	Описание.Вставить("Тесты", Новый Массив);
	Описание.Вставить("Ошибки", Новый Массив);
	Описание.Вставить("НастройкиВыполнения", Новый Структура());
	
	Возврат Описание;
	
КонецФункции

// ОписаниеТеста
//  Возвращает описание регистрируемого теста.
//  Эта структура используется на этапе формирования описаний имеющихся тестов
// Параметры:
//  Имя - Строка - Имя тестового метода
//  Представление - Строка - Представление, краткое описание теста
//  КонтекстыВызова - Массив из Строка - Контексты исполнения теста
//  ТегиСтрокой - Строка - Теги теста
// 
// Возвращаемое значение:
//  Структура - Описание теста:
// * Имя - Строка - Имя теста (тестового метода)
// * Представление - Строка - Представление теста
// * Теги - Массив из Строка - Теги теста
// * КонтекстВызова - Массив из Строка - Контексты исполнения теста
// * НастройкиВыполнения- Структура - Настройки исполнения теста
// * Параметры - Неопределено, Массив из Произвольный - Параметры теста
Функция ОписаниеТеста(Имя, Представление, КонтекстыВызова, Знач ТегиСтрокой = "") Экспорт
	
	Если ТегиСтрокой = Неопределено Тогда
		ТегиСтрокой = "";
	КонецЕсли;
	
	Описание = Новый Структура();
	Описание.Вставить("Имя", Строка(Имя));
	Описание.Вставить("Представление", Строка(Представление));
	Описание.Вставить("Теги", СтрРазделить(ТегиСтрокой, ", ", Ложь));
	Описание.Вставить("КонтекстВызова", КонтекстыВызова);
	Описание.Вставить("НастройкиВыполнения", Новый Структура());
	Описание.Вставить("Параметры", Неопределено);
	
	Возврат Описание;
	
КонецФункции

// ОписаниеИсполняемогоНабораТестов
//  Возвращает описание исполняемого тестового набора.
//  Содержит данные необходимые для выполнения прогона тестов
// Параметры:
//  НаборТестов - Массив из см. ОписаниеТеста - Коллекция зарегистрированных тестовых наборов, см. ОписаниеТестовогоНабора
//  ТестовыйМодуль - Структура - Описание тестового модуля, см. ЮТФабрика.ОписаниеМодуля
// 
// Возвращаемое значение:
//  Структура - Описание исполняемого набора тестов:
// * Имя - Строка - Имя набора
// * Представление - Строка - Представление набора
// * Теги - Массив из Строка - Тэги набора
// * Ошибки - Массив из см. ЮТФабрика.ОписаниеВозникшейОшибки - Описания ошибок выполнения теста
// * Режим - Строка - Режим исполнения набора, см. КонтекстыВызова
// * МетаданныеМодуля - см. ЮТФабрика.ОписаниеМодуля
// * Тесты - Массив из см. ОписаниеИсполняемогоТеста - Тесты набора, структуру тестов см. ОписаниеИсполняемогоТеста
// * Выполнять - Булево - Признак, что можно выполнить прогон набора (нет ошибок блокирующих запуск) 
// * ДатаСтарта - Число - Дата запуска набора
// * Длительность - Число - Продолжительность выполнения набора
// * НастройкиВыполнения- Структура - Настройки исполнения теста
Функция ОписаниеИсполняемогоНабораТестов(НаборТестов, ТестовыйМодуль) Экспорт
	
	Описание = Новый Структура();
	
	Описание.Вставить("Имя", НаборТестов.Имя);
	Описание.Вставить("Представление", НаборТестов.Представление);
	Описание.Вставить("Теги", НаборТестов.Теги);
	Описание.Вставить("Ошибки", ЮТОбщий.СкопироватьМассив(НаборТестов.Ошибки));
	Описание.Вставить("Режим", "");
	Описание.Вставить("МетаданныеМодуля", ТестовыйМодуль.МетаданныеМодуля);
	Описание.Вставить("Тесты", Новый Массив);
	Описание.Вставить("Выполнять", Описание.Ошибки.Количество() = 0);
	Описание.Вставить("ДатаСтарта", 0);
	Описание.Вставить("Длительность", 0);
	Описание.Вставить("НастройкиВыполнения", НаборТестов.НастройкиВыполнения);
	
	Возврат Описание;
	
КонецФункции

// ОписаниеИсполняемогоТеста
//  Возвращает описание исполняемого теста
//  Содержит данные необходимые для выполнения прогона тестов
// 
// Параметры:
//  Тест - Структура из см. ОписаниеТеста - Описание зарегистрированного теста, см. ОписаниеТеста
//  Режим - Строка - Режим исполнения, см. КонтекстыВызова
//  ТестовыйМодуль - Структура - Описание тестового модуля, см. ЮТФабрика.ОписаниеМодуля
// 
// Возвращаемое значение:
//  Структура - Описание исполняемого теста:
// * Имя - Строка - Имя/представление теста
// * Метод - Строка - Имя тестового метода
// * ПолноеИмяМетода - Строка - Полное имя тестового метода, ИмяМодуля.ИмяМетода
// * Теги - Массив из Строка - Теги теста
// * Режим - Строка - Режим исполнения теста, см. КонтекстыВызова
// * ДатаСтарта - Число - Дата запуска теста
// * Длительность - Число - Продолжительность выполнения теста
// * Статус - Строка - Статус выполнения теста, см. СтатусыИсполненияТеста
// * Ошибки - Массив из см. ЮТФабрика.ОписаниеВозникшейОшибки - Описания ошибок выполнения теста, см. ЮТФабрика.ОписаниеВозникшейОшибки
// * НастройкиВыполнения- Структура - Настройки исполнения теста
Функция ОписаниеИсполняемогоТеста(Тест, Режим, ТестовыйМодуль) Экспорт
	
	ПолноеИмяМетода = СтрШаблон("%1.%2", ТестовыйМодуль.МетаданныеМодуля.Имя, Тест.Имя);
	Представление = ПредставлениеТеста(Тест);
	
	ОписаниеТеста = Новый Структура;
	ОписаниеТеста.Вставить("Имя", Представление);
	ОписаниеТеста.Вставить("Метод", Тест.Имя);
	ОписаниеТеста.Вставить("ПолноеИмяМетода", ПолноеИмяМетода);
	ОписаниеТеста.Вставить("Теги", Тест.Теги);
	ОписаниеТеста.Вставить("Режим", Режим);
	ОписаниеТеста.Вставить("ДатаСтарта", 0);
	ОписаниеТеста.Вставить("Длительность", 0);
	ОписаниеТеста.Вставить("Статус", СтатусыИсполненияТеста().Ожидание);
	ОписаниеТеста.Вставить("Ошибки", Новый Массив);
	ОписаниеТеста.Вставить("НастройкиВыполнения", Тест.НастройкиВыполнения);
	ОписаниеТеста.Вставить("Параметры", Тест.Параметры);
	
	Возврат ОписаниеТеста;
	
КонецФункции

// ПараметрыЗапуска
//  Набор параметров подсистемы тестирования. 
//  Параметры на английском, чтобы не держать несколько реализаций чтения и обработки параметров
// 
// Возвращаемое значение:
//  Структура - Параметры:
// * ВыполнятьМодульноеТестирование - Булево - Признак необходимости выполнения тестов
// * reportPath - Строка - Файл или каталог сохранения отчета о тестировании
// * filter - Структура - Параметры отбора запускаемых тестов, см. ПараметрыФильтрации
// * settings - см. НастройкиВыполнения
// * closeAfterTests - Булево - Признак необходимости закрытия приложения по окончании прогона
// * reportFormat - Строка - Формат отчета о тестировании. 
//                  Модули реализующие различные форматы отчетов собраны в подсистеме ЮТФормированиеОтчета
Функция ПараметрыЗапуска() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ВыполнятьМодульноеТестирование", Ложь);
	
	Параметры.Вставить("reportPath", "");
	Параметры.Вставить("closeAfterTests", Истина);
	Параметры.Вставить("filter", ПараметрыФильтрации());
	Параметры.Вставить("settings", НастройкиВыполнения());
	Параметры.Вставить("reportFormat", "jUnit");
	Параметры.Вставить("showReport", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// ОписаниеКонтекстаОшибки
//  Возвращает описание деталей/расшифровки ошибки
// Возвращаемое значение:
//  Структура - Детали ошибки:
// * ОшибкаУтверждения - Булево - Признак, это ошибка проверки утверждения
// * ПроверяемоеЗначение - Произвольный - Фактическое значение
// * ОжидаемоеЗначение - Произвольный - Ожидаемое значение
Функция ОписаниеКонтекстаОшибки() Экспорт
	
	Описание = Новый Структура("ПроверяемоеЗначение, ОжидаемоеЗначение");
	Описание.Вставить("ОшибкаУтверждения", Ложь);
	
	Возврат Описание;
	
КонецФункции

// ОписаниеМодуля
//  Возвращает структуру описания модуля
// Возвращаемое значение:
//  Структура - Описание модуля:
// * Имя - Строка - Имя модуля
// * ПолноеИмя - Строка - Полное имя модуля
// * Расширение - Строка - Имя расширения, владельца модуля
// * КлиентУправляемоеПриложение - Булево - Доступность контекста 
// * КлиентОбычноеПриложение - Булево - Доступность контекста
// * Сервер - Булево - Доступность контекста
// * ВызовСервера - Булево - Доступность контекста
Функция ОписаниеМодуля() Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("Имя", "");
	Описание.Вставить("ПолноеИмя", "");
	Описание.Вставить("Расширение", "");
	Описание.Вставить("КлиентУправляемоеПриложение", Ложь);
	Описание.Вставить("КлиентОбычноеПриложение", Ложь);
	Описание.Вставить("Сервер", Ложь);
	Описание.Вставить("ВызовСервера", Ложь);
	
	Возврат Описание;
	
КонецФункции

// ОписаниеВозникшейОшибки
//  Возвращает базовую структуру ошибки
// 
// Параметры:
//  Сообщение - Строка
// 
// Возвращаемое значение:
//  Структура - Описание возникшей ошибки:
// * Сообщение - Строка - Описание возникшей ошибки
// * Стек - Строка - Стек возникшей ошибки
// * ТипОшибки - Строка - Тип возникшей ошибки. Доступные значения см. ЮТФабрика.ТипыОшибок
Функция ОписаниеВозникшейОшибки(Сообщение) Экспорт
	
	Возврат Новый Структура("Сообщение, Стек, ТипОшибки", Сообщение);
	
КонецФункции

// Возвращает базовую структуру ошибки проверки факта и ожидания
// 
// Параметры:
//  Сообщение - Строка
// 
// Возвращаемое значение:
//  Структура - Описание возникшей ошибки:
// * Сообщение - Строка - Описание возникшей ошибки
// * Стек - Строка - Стек возникшей ошибки
// * ТипОшибки - Строка - Тип возникшей ошибки. Доступные значения см. ЮТФабрика.ТипыОшибок
// * ПроверяемоеЗначение - Произвольный - Проверяемое, фактическое значение
// * ОжидаемоеЗначение - Произвольный - Ожидаемое значение
Функция ОписаниеОшибкиСравнения(Сообщение) Экспорт
	
	Описание = ОписаниеВозникшейОшибки(Сообщение);
	Описание.ТипОшибки = ТипыОшибок().Утверждений;
	Описание.Вставить("ПроверяемоеЗначение");
	Описание.Вставить("ОжидаемоеЗначение");
	
	Возврат Описание;
	
КонецФункции

// Возвращает базовую структуру ошибки пропуска теста
// 
// Параметры:
//  Сообщение - Строка
// 
// Возвращаемое значение:
//  Структура -- Описание возникшей ошибки::
// * Сообщение - Строка - Описание возникшей ошибки
// * Стек - Строка - Стек возникшей ошибки
// * ТипОшибки - Строка - Тип возникшей ошибки. Доступные значения 
//   См. ЮТФабрика.ТипыОшибок
Функция ОписаниеОшибкиПропуска(Сообщение) Экспорт
	
	Описание = ОписаниеВозникшейОшибки(Сообщение);
	Описание.ТипОшибки = ТипыОшибок().Пропущен;
	
	Возврат Описание;
	
КонецФункции

// Описание события исполнения тестов.
// 
// Параметры:
//  Модуль - см. ЮТФабрика.ОписаниеТестовогоМодуля
//  Набор - см. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
//  Тест - см. ЮТФабрика.ОписаниеИсполняемогоТеста
// 
// Возвращаемое значение:
//  Структура - Описание события исполнения тестов:
// * ИмяМодуля - Строка
// * Набор - см. ЮТФабрика.ОписаниеИсполняемогоНабораТестов
// * Тест - см. ЮТФабрика.ОписаниеИсполняемогоТеста
Функция ОписаниеСобытияИсполненияТестов(Модуль, Набор = Неопределено, Тест = Неопределено) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("Модуль", Модуль);
	Описание.Вставить("Набор", Набор);
	Описание.Вставить("Тест", Тест);
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

// КонтекстыМодуля
//  Возвращает коллекцию доступных контекстов выполнения модуля
// Параметры:
//  Модуль Модуль
// 
// Возвращаемое значение:
//  Массив - Контексты модуля
Функция КонтекстыМодуля(Модуль) Экспорт
	
	Контексты = КонтекстыВызова();
	
	КонтекстыМодуля = Новый Массив();
	
	Если Модуль.Сервер Тогда
		КонтекстыМодуля.Добавить(Контексты.Сервер);
	КонецЕсли;
	
	Если Модуль.КлиентУправляемоеПриложение Тогда
		КонтекстыМодуля.Добавить(Контексты.КлиентУправляемоеПриложение);
	КонецЕсли;
	
	Если Модуль.КлиентОбычноеПриложение Тогда
		КонтекстыМодуля.Добавить(Контексты.КлиентОбычноеПриложение);
	КонецЕсли;
	
	Возврат КонтекстыМодуля;

КонецФункции

// КонтекстыПриложения
//  Возвращает коллекцию доступных контекстов приложения
// Возвращаемое значение:
//  Массив из см. КонтекстыВызова - Контексты приложения
Функция КонтекстыПриложения() Экспорт

#Если Сервер Тогда
	ВызватьИсключение "Метод получения контекстов приложения должен вызываться с клиента";
#КонецЕсли

	Контексты = КонтекстыВызова();
	КонтекстыПриложения = Новый Массив();
	
	КонтекстыПриложения.Добавить(Контексты.Сервер);
	КонтекстыПриложения.Добавить(Контексты.ВызовСервера);
	
#Если ТолстыйКлиентОбычноеПриложение Тогда
	КонтекстыПриложения.Добавить(Контексты.КлиентОбычноеПриложение);
#ИначеЕсли ТолстыйКлиентУправляемоеПриложение Или ТонкийКлиент Тогда
	КонтекстыПриложения.Добавить(Контексты.КлиентУправляемоеПриложение);
#КонецЕсли
	
	Возврат КонтекстыПриложения;
	
КонецФункции

// КонтекстИсполнения
//  Возвращает контекст исполнения по контексту вызова
// Параметры:
//  КонтекстВызова - Строка - Контекст вызова, см. ЮТФабрика.КонтекстыВызова
// 
// Возвращаемое значение:
//  Неопределено, Строка - Контекст исполнения
Функция КонтекстИсполнения(КонтекстВызова) Экспорт
	
	КонтекстыВызова = КонтекстыВызова();
	
	Если КонтекстВызова = КонтекстыВызова.Сервер Тогда
		
		Возврат КонтекстыИсполнения().Сервер;
		
	ИначеЕсли КонтекстВызова = КонтекстыВызова.КлиентУправляемоеПриложение
		ИЛИ КонтекстВызова = КонтекстыВызова.КлиентОбычноеПриложение
		ИЛИ КонтекстВызова = КонтекстыВызова.ВызовСервера Тогда
		
		Возврат КонтекстыИсполнения().Клиент;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Формирует представление теста
// 
// Параметры:
//  Тест - см. ОписаниеТеста
// 
// Возвращаемое значение:
//  Строка - Представление теста
Функция ПредставлениеТеста(Тест) Экспорт
	
	Если ЗначениеЗаполнено(Тест.Представление) Тогда
		Представление = Тест.Представление;
	ИначеЕсли ЗначениеЗаполнено(Тест.Параметры) Тогда
		ПредставлениеПараметров = СтрСоединить(Тест.Параметры, ", ");
		Представление = СтрШаблон("%1(%2)", Тест.Имя, ПредставлениеПараметров);
	Иначе
		Представление = Тест.Имя;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

/////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции, составляющие внутреннюю реализацию модуля 
///////////////////////////////////////////////////////////////////////////////// 
#Область СлужебныеПроцедурыИФункции

// ПараметрыФильтрации
// 	Возвращает структуру отборов для поиска тестов
// Возвращаемое значение:
//  Структура - Параметры фильтрации:
// * extensions - Неопределено, Массив из Строка - Список тестовых расширений
// * modules - Неопределено, Массив из Строка - Список тестовых модулей
// * suites - Неопределено, Массив из Строка - Список тестовых наборов
// * paths - Неопределено, Массив из Строка - Список путей до тестовых методов, путь может быть не полным.
// 									Например: 
// 									+ tests - Ищем тесты в расширении tests
// 									+ tests.ОМ_ОбщегоНазначения - Ищем тесты в модуле ОМ_ОбщегоНазначения расширения tests
// 									+ tests.ОМ_ОбщегоНазначения.ПолучитьЗначениеРеквизита - указание конкретного теста
// * tags - Неопределено, Массив из Строка - Список тэгов
// * contexts - Неопределено, Массив из Строка - Список тестируемых контекстов
// * tests - Неопределено, Массив из Строка - Список полных имен тестовых методов. ИмяМодуля.ИмяМетода{.Контекст}
Функция ПараметрыФильтрации()
	
	Параметры = Новый Структура;
	Параметры.Вставить("extensions");
	Параметры.Вставить("modules");
	Параметры.Вставить("suites");
	Параметры.Вставить("tags");
	Параметры.Вставить("contexts");
	Параметры.Вставить("paths");
	Параметры.Вставить("tests");
	
	Возврат Параметры;
	
КонецФункции

Функция НастройкиВыполнения()
	
	ПараметрыИсполнения = ПараметрыИсполненияТеста();
	
	Настройки = Новый Структура();
	
	Настройки.Вставить(ПараметрыИсполнения.ВТранзакции, Ложь);
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти
