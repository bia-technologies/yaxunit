//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// ЗагрузитьТесты
// 	Читает наборы тестов (тестовые модули) из расширений
// Параметры:
//  ПараметрыЗапускаТестов - см. ЮТФабрика.ПараметрыЗапуска
//
// Возвращаемое значение:
//  Массив из см. ЮТФабрикаСлужебный.ОписаниеТестовогоМодуля - Набор описаний тестовых модулей, которые содержат информацию о запускаемых тестах
Функция ЗагрузитьТесты(ПараметрыЗапускаТестов) Экспорт

	ЮТФильтрацияСлужебный.УстановитьКонтекст(ПараметрыЗапускаТестов);
	
	ТестовыеМодули = ТестовыеМодули();
	
	Результат = ТестовыеСценарии(ТестовыеМодули, ПараметрыЗапускаТестов);
	
	ЮТЗависимостиСлужебныйКлиент.ДедупликацияЗависимостей(Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ТестовыеМодули
//  Возвращает описания модулей, содержащих тесты
// Возвращаемое значение:
//  Массив из см. ЮТМетаданныеСлужебныйВызовСервера.МетаданныеМодуля - Тестовые модули
Функция ТестовыеМодули()
	
	Ключи = Новый Структура("ТестовыеМодули, ТестовыеМодулиЗагружены, ОтфильтрованныеМодули, ТекущийМодуль",
							"ТестовыеМодули", "ТестовыеМодулиЗагружены", "ОтфильтрованныеМодули", "ТекущийМодуль");
	
	МодулиРасширения = ЮТМетаданныеСлужебныйВызовСервера.МодулиРасширений();
	МодулиДвижка = ЮТМетаданныеСлужебныйВызовСервера.ИменаМодулейДвижка();
	
	ТестовыеМодули = ЮТИсполнительСлужебныйКлиент.ЗначениеСостоянияТекущегоЭтапаИсполнения(Ключи.ТестовыеМодули);
	
	Если ЮТИсполнительСлужебныйКлиент.ЗначениеСостоянияТекущегоЭтапаИсполнения(Ключи.ТестовыеМодулиЗагружены, Ложь) Тогда
		Возврат ТестовыеМодули;
	КонецЕсли;
	
	ОтфильтрованныеМодули = ЮТИсполнительСлужебныйКлиент.ЗначениеСостоянияТекущегоЭтапаИсполнения(Ключи.ОтфильтрованныеМодули);
	
	Если ОтфильтрованныеМодули = Неопределено Тогда
		
		ОтфильтрованныеМодули = Новый Массив;
		
		Для Каждого ОписаниеМодуля Из МодулиРасширения Цикл
			
			Если МодулиДвижка.Найти(ОписаниеМодуля.Имя) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЮТЛогирование.Отладка("Анализ модуля: " + ОписаниеМодуля.Имя);
			
			Если НЕ ЮТФильтрацияСлужебный.ЭтоПодходящийМодуль(ОписаниеМодуля) Тогда
				ЮТЛогирование.Отладка("	Пропущен, не подходит под отбор");
			Иначе
				ОтфильтрованныеМодули.Добавить(ОписаниеМодуля);
			КонецЕсли;
			
		КонецЦикла;
		
		ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ОтфильтрованныеМодули, ОтфильтрованныеМодули);
		
	КонецЕсли;
	
	ОшибочныйМодуль = ЮТИсполнительСлужебныйКлиент.ЗначениеСостоянияТекущегоЭтапаИсполнения(Ключи.ТекущийМодуль);
	ПропуститьМодуль = ОшибочныйМодуль <> Неопределено;
	
	Если ТестовыеМодули = Неопределено Тогда
		ТестовыеМодули = Новый Массив;
		ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ТестовыеМодули, ТестовыеМодули);
	КонецЕсли;
	
	Для Каждого ОписаниеМодуля Из ОтфильтрованныеМодули Цикл
		
		Если ПропуститьМодуль Тогда
			Если ОписаниеМодуля.Имя = ОшибочныйМодуль Тогда
				ПропуститьМодуль = Ложь;
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ТекущийМодуль, ОписаниеМодуля.Имя);
		
		Если НЕ ЮТЧитательСлужебный.ЭтоТестовыйМодуль(ОписаниеМодуля) Тогда
			ЮТЛогирование.Отладка("	Пропущен, это не тестовый модуль");
		Иначе
			ЮТЛогирование.Отладка("	Добавлен");
			ТестовыеМодули.Добавить(ОписаниеМодуля);
		КонецЕсли;
		
	КонецЦикла;
	
	ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ТестовыеМодулиЗагружены, Истина);
	ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ТекущийМодуль, Неопределено);
	Возврат ТестовыеМодули;
	
КонецФункции

Функция ТестовыеСценарии(ТестовыеМодули, ПараметрыЗапускаТестов)
	
	Ключи = Новый Структура("ЗарегистрированныеСценарииМодуля, ТекущийМодуль", "ЗарегистрированныеСценарииМодуля", "ТекущийМодуль");
	Результат = ЮТИсполнительСлужебныйКлиент.ЗначениеСостоянияТекущегоЭтапаИсполнения(Ключи.ЗарегистрированныеСценарииМодуля);
	
	Если Результат = Неопределено Тогда
		Результат = Новый Массив;
		ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ЗарегистрированныеСценарииМодуля, Результат);
	КонецЕсли;
	
	ОшибочныйМодуль = ЮТИсполнительСлужебныйКлиент.ЗначениеСостоянияТекущегоЭтапаИсполнения(Ключи.ТекущийМодуль);
	Если ОшибочныйМодуль <> Неопределено Тогда
		ТекстОшибки = СтрШаблон("Обнаружена ошибка загрузки сценариев модуля `%1`, детали ошибки смотрите выше.", ОшибочныйМодуль);
		ЮТОбщий.СообщитьПользователю(ТекстОшибки);
		ЮТЛогирование.Ошибка(ТекстОшибки);
	КонецЕсли;
	
	ПропуститьМодуль = ОшибочныйМодуль <> Неопределено;
	
	Для Каждого ОписаниеМодуля Из ТестовыеМодули Цикл
		
		Если ПропуститьМодуль Тогда
			Если ОписаниеМодуля.Имя = ОшибочныйМодуль Тогда
				ПропуститьМодуль = Ложь;
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		ЮТИсполнительСлужебныйКлиент.СохранитьСостояниеТекущегоЭтапаИсполнения(Ключи.ТекущийМодуль, ОписаниеМодуля.Имя);
		ОписаниеТестовогоМодуля = ЗарегистрированныеСценарииМодуля(ОписаниеМодуля, ПараметрыЗапускаТестов);
		
		Если ОписаниеТестовогоМодуля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ОписаниеТестовогоМодуля);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ЗарегистрированныеСценарииМодуля(МетаданныеМодуля, ПараметрыЗапуска)
	
	// TODO Фильтрация по путям
	ОписаниеМодуля = Неопределено;
	
#Если ТолстыйКлиентОбычноеПриложение Тогда
	Если МетаданныеМодуля.КлиентОбычноеПриложение Тогда
		ОписаниеМодуля = ЮТЧитательСлужебный.ИсполняемыеСценарииМодуля(МетаданныеМодуля);
	ИначеЕсли МетаданныеМодуля.Сервер Тогда
		ОписаниеМодуля = ЮТЧитательСлужебныйВызовСервера.ИсполняемыеСценарииМодуля(МетаданныеМодуля);
		ЮТЛогированиеСлужебный.ВывестиСерверныеСообщения();
	КонецЕсли;
#ИначеЕсли ТолстыйКлиентУправляемоеПриложение Или ТонкийКлиент Тогда
	Если МетаданныеМодуля.КлиентУправляемоеПриложение Тогда
		ОписаниеМодуля = ЮТЧитательСлужебный.ИсполняемыеСценарииМодуля(МетаданныеМодуля);
	ИначеЕсли МетаданныеМодуля.Сервер Тогда
		ОписаниеМодуля = ЮТЧитательСлужебныйВызовСервера.ИсполняемыеСценарииМодуля(МетаданныеМодуля);
		ЮТЛогированиеСлужебный.ВывестиСерверныеСообщения();
	КонецЕсли;
#ИначеЕсли Сервер Тогда
	Если МетаданныеМодуля.Сервер Тогда
		ОписаниеМодуля = ЮТЧитательСлужебный.ИсполняемыеСценарииМодуля(МетаданныеМодуля);
	Иначе
		ВызватьИсключение "Чтение списка тестов модуля в недоступном контексте";
	КонецЕсли;
#КонецЕсли
	
	Возврат ОписаниеМодуля;
	
КонецФункции

#КонецОбласти
