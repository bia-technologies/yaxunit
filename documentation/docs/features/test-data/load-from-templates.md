---
tags: [Начало, Тестовые данные]
sidebar_position: 3
---

# Загрузка данных из макетов

## Описание

Для загрузки данных из макетов подойдет метод [`ЮТест.Данные().ЗагрузитьИзМакета`](/api/ЮТТестовыеДанные#загрузитьизмакета)

Он позволяет:

* Загружать данные из табличных макет (`ТабличныйДокумент` или `ТекстовыйДокумент` с таблицей markdown)
* Загружать данные на клиенте и на сервер
* Загружать не весь макет, а конкретную область (в одном макете может находится несколько таблиц данных)
* Приводить значения к указанным типам и создавать записи в базе данных

Пример табличного документа:

| Товар   | Товар.Поставщик | Товар.Вид | Цена  | Количество | Сумма |
|---------|-----------------|-----------|-------|------------|-------|
| Товар 1 | Поставщик 1     | Товар     | 100   | 1          | 100   |
| Товар 1 |                 |           | 100   | 2          | 200   |
| Товар 1 | Поставщик 2     | Товар     | 100   | 3          | 300   |
| Товар 2 | Поставщик 1     | Товар     | 2 000 | 1          | 2000  |
| Услуга  |                 | Услуга    | 300,5 | 1          | 300.5 |

```bsl
ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));

ЮТест.Данные().ЗагрузитьИзМакета("ОбщийМакет.МакетТестовыхДанных", ОписанияТипов);
```

### Синтаксис

```bsl
Функция ЗагрузитьИзМакета(
    Макет,
    ОписанияТипов,
    КэшЗначений = Неопределено,
    ЗаменяемыеЗначения = Неопределено,
    ПараметрыСозданияОбъектов = Неопределено) Экспорт
```

### Параметры

| Параметр | Тип | Обязательный | Описание |
|----------|-----|--------------|----------|
| `Макет` | `ТабличныйДокумент`, `ТекстовыйДокумент`, `Строка` | Да | Источник данных:<br />- Макет<br />- [Имя макета](#форматы-имени-макета) конфигурации<br />- Markdown-таблица |
| `ОписанияТипов` | `Соответствие`, `Структура` | Да | Соответствие `ИмяКолонки ➝ ОписаниеТипов` |
| `КэшЗначений` | `Соответствие` | Нет | Кэш для хранения созданных объектов (повторное использование) |
| `ЗаменяемыеЗначения` | `Соответствие` | Нет | Подмена значений в формате `Значение ячейки макета ➝ Данные базы` |
| `ПараметрыСозданияОбъектов` | `Структура` | Нет | Настройки создания объектов (см. `ЮТФабрика.ПараметрыСозданияОбъектов`) |

### Форматы имени макета

| Описание | Пример |
|----------|-----|
| Общий макет конфигурации |`"ОбщийМакет.МакетТовары"` |
| Именованная область общего макета |`"ОбщийМакет.МакетТовары.ОбластьСпецификация"` |
| Макет обработки |`"Обработка.ИмпортДанных"` |
| Область макета обработки по координатам |`"Обработка.ИмпортДанных.R2C1:R5C11"` |
| Непосредственно данные в формате Markdown |`"\| Товар \| Количество \| Цена \|..."` |

### Особенности

* Данные в базе всегда создаются, не выполняется поиск уже существующих ссылок. При необходимости вы можете найти данные сами и передать в параметрах `ЗаменяемыеЗначения`
  :::tip
  При преобразовании табличного документа в таблицу значений существующие объекты метаданных не изменяются, только создаются новые. Нужно проявлять внимательность в случаях, если в базе уже имеются данные с тем же кодом/наименованием (и по ним используется контроль уникальности), что и в табличном документе.
  :::
* Можно указывать значения вложенных реквизитов, например при создании товаров (колонка `Товар`) необходимо указать конкретные значения поставщика и вида товара. Для этого необходимо добавить колонки `Товар.Поставщик` и `Товар.Вид`, в которых указать нужные значения.
* Переиспользование созданных данных, используется `КэшЗначений`, в который помещаются все созданные данные, если кэш содержит данные подходящего типа с указанным идентификатором, то используется ранее созданное значение из кэша. Например, в примере выше, для первой строки будет создан товар с идентификатором `Товар 1`. В строках 2 и 3 будет использовано значение из кэша - `Товар 1`, в 4й и 5й строке - созданы новые товары.

  :::tip
  Кэш значений может передаваться между загрузками разных макетов, например есть макет с табличной частью документа и второй макет с набором ожидаемых движений. Чтобы созданные товары из табличной части соответствовали товарам движений необходимо передать `КэшЗначений` между загрузками.

  ```bsl
  КэшЗначений = Неопределено;
  ТабличнаяЧасть = ЮТест.Данные().ЗагрузитьИзМакета("ОбщийМакет.ДанныеДокумента.ТабличнаяЧасть", ОписанияТиповТЧ, КэшЗначений);
  ОжидаемыеДвижения = ЮТест.Данные().ЗагрузитьИзМакета("ОбщийМакет.ДанныеДокумента.ДвиженияРегистра1", ОписанияТиповРегистра1, КэшЗначений);
  ```

  :::

* Значение в колонке ссылочного типа является идентификатором ссылки. Для справочника значение этой колонки по умолчанию записывается в реквизит "Наименование" или "Код", в зависимости от того, какой из этих реквизитов является основным представлением. Для документа это значение никуда не записывается, оно лишь является идентификатором. Таким образом, если одному значению в колонке "Товар" соответствуют различные значения в других колонках, то в элемент справочника запишутся реквизиты из первой строки, а для других строк ссылка на этот элемент будет проставлена по соответствующему идентификатору
* Если загружаемый макет соответствует какой-либо таблице базы (справочник, документ, регистр или табличная часть), то описание типов можно получить с помощью метода [`ЮТМетаданные.ТипыРеквизитов`](/api/ЮТМетаданные#типыреквизитов)

  ```bsl
  ОписанияТипов = ЮТМетаданные.ТипыРеквизитов("Документ.Продажи.Товары", Истина);
  ```

## Примеры

### Загрузка всего макета

В качестве источника данных можно использовать весь табличный документ. В этом случае будет получена таблица значений, начиная с 1-ой строки и 1-ой колонки табличного документа до первой пустой строки.

_Исходные данные_

| Товар   | Товар.Поставщик | Товар.Вид | Цена  | Количество | Сумма |
|---------|-----------------|-----------|-------|------------|-------|
| Товар 1 | Поставщик 1     | Товар     | 100   | 1          | 100   |
| Товар 1 |                 |           | 100   | 2          | 200   |
| Товар 1 | Поставщик 2     | Товар     | 100   | 3          | 300   |
| Товар 2 | Поставщик 1     | Товар     | 2 000 | 1          | 2000  |
| Услуга  |                 | Услуга    | 300,5 | 1          | 300.5 |

:::info
Этот код работает и на клиенте и на сервер. Для сервера результатом будет таблица значений, для клиента - массив структур
:::

```bsl
ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));

ТаблицаТоваров = ЮТест.Данные().ЗагрузитьИзМакета("ОбщийМакет.ЮТ_МакетТестовыхДанных", ОписанияТипов);

ЮТест.ОжидаетЧто(ТаблицаТоваров)
    .ИмеетТип("ТаблицаЗначений")
    .ИмеетДлину(5)
    .Свойство("[0].Товар.Наименование").Равно("Товар 1")
    .Свойство("[0].Товар.Поставщик.Наименование").Равно("Поставщик")
    .Свойство("[0].Товар.Вид").Равно(Перечисления.ВидыТоваров.Товар)
    .Свойство("[0].Количество").Равно(1)
    .Свойство("[0].Цена").Равно(100)
    .Свойство("[0].Сумма").Равно(100)
    .Свойство("[1].Товар").Равно(ТаблицаТоваров[0].Товар)
    .Свойство("[1].Количество").Равно(2)
    .Свойство("[1].Цена").Равно(100)
    .Свойство("[1].Сумма").Равно(200)
    .Свойство("[2].Товар").Равно(ТаблицаТоваров[0].Товар)
    .Свойство("[2].Количество").Равно(3)
    .Свойство("[2].Цена").Равно(100)
    .Свойство("[2].Сумма").Равно(300)
    .Свойство("[3].Товар.Наименование").Равно("Товар 2")
    .Свойство("[3].Товар.Поставщик.Наименование").Равно("Поставщик")
    .Свойство("[3].Товар.Вид").Равно(Перечисления.ВидыТоваров.Товар)
    .Свойство("[3].Количество").Равно(1)
    .Свойство("[3].Цена").Равно(2000)
    .Свойство("[3].Сумма").Равно(2000)
    .Свойство("[4].Товар.Наименование").Равно("Услуга")
    .Свойство("[4].Товар.Поставщик").НеЗаполнено()
    .Свойство("[4].Товар.Вид").Равно(Перечисления.ВидыТоваров.Услуга)
    .Свойство("[4].Количество").Равно(1)
    .Свойство("[4].Цена").Равно(300.5)
    .Свойство("[4].Сумма").Равно(300.5);
```

### Загрузка области из табличного макета

Можно получить отдельную область из табличного документа и использовать ее в качестве источника данных для таблицы значений. Таким образом в одном табличном документе можно хранить несколько таблиц.

Пример юнит-теста для проверки работы метода. На входе он получает таблицу значений, которую заполняет и/или возвращает. В одной области табличного документа - исходные данные, передаваемые в качестве входящего параметра, а в другой - таблица с ожидаемым значением.

_Исходные данные_

| Товар   | Товар.Поставщик | Товар.Вид  | Количество |
|---------|-----------------|------------|------------|
| Товар 1 | Поставщик 1     | Товар      | 1          |
| Товар 2 | Поставщик 1     | Товар      | 1          |
| Услуга  |                 | Услуга     | 1          |
| Товар   | Цена            | Количество | Сумма      |
| Товар 1 | 100             | 1          | 100        |
| Товар 2 | 2 000           | 1          | 2000       |
| Услуга  | 300,5           | 1          | 300.5      |

```bsl
ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));

// Для общего использования созданных значений используется переменная КешЗначений.
// В двух табличных документах одинаковый состав товаров. 
// Чтобы в обеих таблицах этому товару соответствовало одно значение справочника - 
// нужно передавать в параметре функции переменную, в которой будут храниться соответствия
// наименований к ссылкам на объекты. 
КэшЗначений = Новый Соответствие;

ТаблицаИсходныхДанных = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.R1C1R4C4",
    ОписанияТипов,
    КэшЗначений
);

ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));

ТаблицаОжидаемыхЗначений = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.R5C1R8C4", 
    ОписанияТипов,
    КэшЗначений
);

ТаблицаРезультата = ОбщийМодуль1.ТаблицаТоваровСЦенамиИзОстатков(ТаблицаИсходныхДанных);
    
ЮТест.ОжидаетЧто(ТаблицаРезультата)
    .Равно(ТаблицаОжидаемыхЗначений);
```

### Загрузка области из текстового макета

Отдельные области можно получать также и из тектового документа, что позволяет хранить несколько markdown-таблиц в одном макете. Чтобы разбить текстовый документ на отдельные именованные области, следует использовать инструкции `#Область...#КонецОбласти` аналогично тому, как они используются в модулях конфигураций.

Пример юнит-теста для проверки работы метода. На входе он получает таблицу значений, которую заполняет и/или возвращает. В одной области табличного документа - исходные данные, передаваемые в качестве входящего параметра, а в другой - таблица с ожидаемым значением.

_Исходные данные_

```txt
#Область ИсходныеДанные
| Товар   | Товар.Поставщик | Товар.Вид  | Количество |
|---------|-----------------|------------|------------|
| Товар 1 | Поставщик 1     | Товар      | 1          |
| Товар 2 | Поставщик 1     | Товар      | 1          |
| Услуга  |                 | Услуга     | 1          |
#КонецОбласти
#Область Результат
| Товар   | Цена            | Количество | Сумма      |
| Товар 1 | 100             | 1          | 100        |
| Товар 2 | 2 000           | 1          | 2000       |
| Услуга  | 300,5           | 1          | 300.5      |
#КонецОбласти
```

```bsl
ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));

КэшЗначений = Новый Соответствие;

ТаблицаИсходныхДанных = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.ИсходныеДанные",
    ОписанияТипов,
    КэшЗначений
);

ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));

ТаблицаОжидаемыхЗначений = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.Результат", 
    ОписанияТипов,
    КэшЗначений
);

ТаблицаРезультата = ОбщийМодуль1.ТаблицаТоваровСЦенамиИзОстатков(ТаблицаИсходныхДанных);
    
ЮТест.ОжидаетЧто(ТаблицаРезультата)
    .Равно(ТаблицаОжидаемыхЗначений);
```

### Загрузка документа с табличной частью и проверка движений

Интеграционный-тест для проверки формирования движений документа по некоторому регистру. В первой таблице - табличная часть документа. Во второй - реквизиты самого документа. В третьей - ожидаемые движения по регистру, который нужно протестировать.

В макете мы создадим три именованные области с таблицами

Область `Реквизиты_документа`

| ПриходТовара | Дата | Поставщик | Валюта | Склад | Организация |
|--------------|------|-----------|--------|-------|-------------|
| Документ 1   | Дата | Поставщик | Валюта | Склад | Организация |

Область `Табличная_часть_документа`

| Документ   | Товар   | Товар.Поставщик | Товар.Вид | Цена  | Количество | Сумма |
|------------|---------|-----------------|-----------|-------|------------|-------|
| Документ 1 | Товар 1 | Поставщик       | Товар     | 100   | 1          | 100   |
| Документ 1 | Товар 2 | Поставщик       | Товар     | 2 000 | 1          | 2000  |
| Документ 1 | Услуга  |                 | Услуга    | 300,5 | 1          | 300.5 |

Область `Ожидаемые_движения`

| Период | Активность | ВидДвижения | Регистратор | Склад | Товар   | Количество |
|--------|------------|-------------|-------------|-------|---------|------------|
| Дата   | Истина     | Приход      | Документ 1  | Склад | Товар 1 | 1          |
| Дата   | Истина     | Приход      | Документ 1  | Склад | Товар 2 | 1          |
| Дата   | Истина     | Приход      | Документ 1  | Склад | Услуга  | 1          |

```bsl
КэшЗначений = Новый Соответствие;

Организация = ЮТест.КонтекстМодуля().Организация;
Поставщик = ЮТест.Данные().СоздатьЭлемент(Справочники.Контрагенты, "Поставщик");
ДатаДокумента = НачалоДня(ТекущаяДатаСеанса());

// Если нужно не создавать новое значение, а использовать существующее, 
// например, созданное ранее, то можно использовать соответствие - 
// значение из табличного документа к нужному значению.    
ЗаменяемыеЗначения = Новый Соответствие;
ЗаменяемыеЗначения.Вставить("Организация", Организация);
ЗаменяемыеЗначения.Вставить("Поставщик", Поставщик);
ЗаменяемыеЗначения.Вставить("Дата", ДатаДокумента);

ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("ПриходТовара", Новый ОписаниеТипов("ДокументСсылка.ПриходТовара"));
ОписанияТипов.Вставить("Дата", Новый ОписаниеТипов("Дата"));
ОписанияТипов.Вставить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
ОписанияТипов.Вставить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
ОписанияТипов.Вставить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
ОписанияТипов.Вставить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));

ТаблицаДокументов = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.Реквизиты_документа"
    ОписанияТипов,
    КэшЗначений,
    ЗаменяемыеЗначения
);
    
ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Документ", Новый ОписаниеТипов("ДокументСсылка.ПриходТовара"));
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));

ТаблицаТоваров = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.Табличная_часть_документа", 
    ОписанияТипов,
    КэшЗначений,
    ЗаменяемыеЗначения
);

ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Период", Новый ОписаниеТипов("Дата"));
ОписанияТипов.Вставить("Активность", Новый ОписаниеТипов("Булево"));
ОписанияТипов.Вставить("ВидДвижения", Новый ОписаниеТипов("ВидДвиженияНакопления"));
ОписанияТипов.Вставить("Регистратор", Новый ОписаниеТипов("ДокументСсылка.ПриходТовара"));
ОписанияТипов.Вставить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));

ТаблицаДвижений = ЮТест.Данные().ЗагрузитьИзМакета(
    "ОбщийМакет.ЮТ_МакетТестовыхДанных.Ожидаемые_движения", 
    ОписанияТипов,
    КэшЗначений,
    ЗаменяемыеЗначения
);
    
Для Каждого ДанныеДокументов Из ТаблицаДокументов Цикл

    Отбор = Новый Структура("Документ", ДанныеДокументов.ПриходТовара);
    ТоварыДокумента = ТаблицаТоваров.Скопировать(Отбор);
        
    ДокументОбъект = ДанныеДокументов.Документ.ПолучитьОбъект();
    ДокументОбъект.Товары.Загрузить(ТоварыДокумента);
    ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
        
    ТоварныеЗапасы = ДокументОбъект.Движения.ТоварныеЗапасы;
    ТоварныеЗапасы.Прочитать();
    ТаблицаТоварныхЗапасов = ТоварныеЗапасы.Выгрузить();

    Отбор = Новый Структура("Регистратор", ДанныеДокументов.ПриходТовара);
    ОжидаемыеТоварныеЗапасы = ТаблицаДвижений.Скопировать(Отбор);
        
    ЮТест.ОжидаетЧто(ТаблицаТоварныхЗапасов)
        .Равно(ОжидаемыеТоварныеЗапасы);
    
КонецЦикла;
```

### Заменяемые значения

Создание и заполнение объектов двойной вложенности и более (например, "Товар.Поставщик.ВидКонтрагента") не поддерживается. Если есть такая потребность, следует использовать переменную "ЗаменяемыеЗначения". Т.е. следует создать и заполнять нужный объект перед преобразованием табличного документа в таблицу значений, а затем передать его в функцию получения таблицы значений.

```bsl
Поставщик = ЮТест.Данные().КонструкторОбъекта(Справочники.Контрагенты)
   .Установить("ВидКонтрагента", ВидКонтрагента)
   .Записать();
        
// В табличном документе должна быть колонка [Товар.Поставщик] со значением "Некий поставщик".
// В этом случае в таблицу значений попадет значение, полученное из соответствия
ЗаменяемыеЗначения = Новый Соответствие;
ЗаменяемыеЗначения.Вставить("Некий поставщик", Поставщик);
        
ТаблицаЗначений = ЮТест.Данные().ЗагрузитьИзМакета(
   ТабличныйДокумент, 
   ОписанияТипов, 
   Неопределено, 
   ЗаменяемыеЗначения
);

ЮТест.ОжидаетЧто(ТаблицаЗначений)
   .Свойство("[0].Товар.Поставщик.ВидКонтрагента").Равно(ВидКонтрагента);
```

### Заменяемые значения и составные типы

Можно использовать составные типы в колонках таблицы значений, но создание объектов метаданных для такой колонки не поддерживается. Можно воспользоваться соответствием "ЗаменяемыеЗначения", если потребуется в одну колонку записать значения разных типов.

```bsl
ТипАналитики = Новый ОписаниеТипов("СправочникСсылка.Товары,СправочникСсылка.Контрагенты");

ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Аналитика", ТипАналитики);

Поставщик = ЮТест.Данные().СоздатьЭлемент(Справочники.Контрагенты);
Товар = ЮТест.Данные().СоздатьЭлемент(Справочники.Товары);
    
ЗаменяемыеЗначения = Новый Соответствие;
ЗаменяемыеЗначения.Вставить("Аналитика (Поставщик)", Поставщик);
ЗаменяемыеЗначения.Вставить("Аналитика (Товар)", Товар);
        
ТаблицаЗначений = ЮТест.Данные().ЗагрузитьИзМакета(
   ТабличныйДокумент, 
   ОписанияТипов, 
   Неопределено, 
   ЗаменяемыеЗначения
);
    
ЮТест.ОжидаетЧто(ТаблицаЗначений)
   .Свойство("[0].Аналитика").Равно(Поставщик)
   .Свойство("[1].Аналитика").Равно(Товар)
;
```

### Inline макеты, загрузка из Markdown

При необходимости вы можете разместить таблицу с данными прямо в коде.

:::tip
Если вы используете EDT, то удобнее редактировать многострочные строки в `Редакторе запроса`
:::

```bsl
ТаблицаMarkDown = 
"| Товар   | Цена  | Количество | Сумма |
||---------|-------|------------|-------|
|| Товар 1 | 100   | 1          | 100   |
|| Товар 2 | 2 000 | 1          | 2000  |
|| Услуга  | 300,9 | 1          | 300,9 |";

ОписанияТипов = Новый Соответствие;
ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));

Данные = ЮТест.Данные().ЗагрузитьИзМакета(ТаблицаMarkDown, ОписанияТипов);
```

## Загрузка данных из макета в информационную базу

Данные макетов не редко применяются для создания тестового окружения - загрузки данных в информационную базу.

Для упрощения этой задачи были добавлены следующий методы

### Загрузка движений документа

Метод [`ЮТест.Данные().ЗагрузитьДвиженияИзМакета`](/api/ЮТТестовыеДанные#загрузитьдвиженияизмакета) позволяет импортировать тестовые данные из табличных/текстовых макетов в движения документов

#### Синтаксис

```bsl
Процедура ЗагрузитьДвиженияИзМакета(
    Документ, 
    ИмяРегистра, 
    Макет, 
    ОбщиеРеквизиты = Неопределено, 
    ПараметрыЗагрузкиМакетов = Неопределено) Экспорт
```

#### Параметры

| Параметр | Тип | Обязательный | Описание |
|----------|-----|--------------|----------|
| **Документ** | `ДокументСсылка` | Да | Ссылка на документ, к которому привязываются движения |
| **ИмяРегистра** | `Строка` | Да | Короткое или полное имя регистра (например, "ТоварныеЗапасы") |
| **Макет** | `ТабличныйДокумент`, `ТекстовыйДокумент`, `Строка` | Да | Источник данных:<br />- Макет<br />- [Имя макета](#форматы-имени-макета) конфигурации<br />- Данные в формате Markdown |
| **ОбщиеРеквизиты** | `Структура` | Нет | Общие значения для всех записей движений (например, `Новый Структура("Организация, Подразделение", Орг, Подр)`) |
| **ПараметрыЗагрузкиМакетов** | `Структура` | Нет | [Параметры](#параметры-загрузки-макетов) загрузки макетов |

#### Параметры загрузки макетов

Структура `ПараметрыЗагрузкиМакетов` может содержать:

| Параметр | Тип | По умолчанию | Описание |
|----------|-----|--------------|----------|
| `КэшЗначений` | `Соответствие` | `Новый Соответствие` | Кэш для хранения созданных объектов (повторное использование) |
| `ЗаменяемыеЗначения` | `Соответствие` | `Новый Соответствие` | Подмена значений в формате `Значение ячейки макета ➝ Данные базы` |
| `ПараметрыСозданияОбъектов` | `Структура` | [`ЮТФабрика.ПараметрыСозданияОбъектов()`](/api/ЮТФабрика#параметрысозданияобъектов) | Настройки десериализации значений макета |
| `ПараметрыЗаписи` | `Структура` | [`ЮТОбщий.ПараметрыЗаписи()`](/api/ЮТОбщий#параметрызаписи) | Настройки создания основных объектов (записей регистра) |

#### Примеры использования

##### Таблица Markdown

```bsl
Док = ЮТест.Данные().СоздатьДокумент("ОперацияПоУчетуТоваров");

ДанныеМакета = "
|| Товар  | Склад    | Количество | ВидДвижения |
||--------|----------|------------|-------------|
|| Товар1 | Основной | 10         | Приход      |
|| Товар2 | Основной | 20         | Расход      |";

ЮТест.Данные().ЗагрузитьДвиженияИзМакета(
    Док, 
    "РегистрНакопления.ТоварыНаСкладах", 
    ДанныеМакета);
```

##### Использование общего макета

```bsl
Док = Документы.ЗаказПокупателя.СоздатьДокумент();
ОбщиеРеквизиты = Новый Структура("Ответственный", Справочники.Сотрудники.НайтиПоНаименованию("Иванов"));

ЮТест.Данные().ЗагрузитьДвиженияИзМакета(
    Док,
    "Продажи",
    "ОбщийМакет.ТестовыеДанныеПродаж",
    ОбщиеРеквизиты);
```

### Загрузка данных в таблицу информационной базы

**Назначение**  
Метод [`ЮТест.Данные().ЗагрузитьИзМакетаВТаблицу`](/api/ЮТТестовыеДанные#загрузитьизмакетавтаблицу) позволяет напрямую загружать данные из табличных/текстовых макетов в объекты информационной базы (справочники, документы, регистры). Основные сценарии:

* Массовое создание элементов справочников, документов и тд
* Наполнение регистров сведений

#### Синтаксис

```bsl
Процедура ЗагрузитьИзМакетаВТаблицу(
    ИмяТаблицы, 
    Макет, 
    ОбщиеРеквизиты = Неопределено, 
    ПараметрыЗагрузкиМакетов = Неопределено) Экспорт
```

#### Параметры

| Параметр | Тип | Обязательный | Описание |
|----------|-----|--------------|----------|
| **ИмяТаблицы** | `Строка` | Да | Целевой объект в формате: <br />- `Справочник.ИмяСправочника`<br />- `Документ.ИмяДокумента`<br />- `РегистрСведений.ИмяРегистра` |
| **Макет** | `ТабличныйДокумент`, `ТекстовыйДокумент`, `Строка` | Да | Источник данных:<br />- Макет<br />- [Имя макета](#форматы-имени-макета) конфигурации<br />- Данные в формате Markdown |
| **ОбщиеРеквизиты** | `Структура` | Нет | Общие значения для всех записей (например, `Новый Структура("Организация, Подразделение", Орг, Подр)`) |
| **ПараметрыЗагрузкиМакетов** | `Структура` | Нет | [Параметры](#параметры-загрузки-макетов) загрузки макетов |

#### Примеры использования

##### Загрузка справочника

```bsl
Макет = "
    || Наименование | Код   |
    ||--------------|-------|
    || КатегорияА   | CAT-1 |
    || КатегорияБ   | CAT-2 |";

ЮТест.Данные().ЗагрузитьИзМакетаВТаблицу(
    "Справочник.КатегорииТоваров", 
    Макет);
```

##### Создание документов с общими реквизитами

```bsl
ОбщиеРеквизиты = Новый Структура("Организация, Проведен", Организация, Истина);
Параметры = ЮТест.Данные().ПараметрыЗагрузкиМакетов();
Параметры.ЗаменяемыеЗначения.Вставить("Поставщик1", Контрагент);

ЮТест.Данные().ЗагрузитьИзМакетаВТаблицу(
    "Документ.ПриходнаяНакладная",
    "Обработка.ТестовыеДанные.МакетНакладных",
    ОбщиеРеквизиты,
    Параметры);
```

##### Импорт данных в регистр сведений

```bsl
ИмяТаблицы = "РегистрСведений.КурсыВалют";
Макет =
    "| Валюта        | Курс |
    ||----------     |------|
    || Ракушки каури | 1    |
    || Камень Раи    | 2    |
    || Нотгельд      | 3    |";

Валюта = ЮТест.Данные().СоздатьЭлемент("Справочник.Валюты");
ПараметрыЗагрузкиМакетов = ЮТест.Данные().ПараметрыЗагрузкиМакетов();
ПараметрыЗагрузкиМакетов.ЗаменяемыеЗначения.Вставить("Камень Раи", Валюта);

ОбщиеРеквизиты = Новый Структура("Период", ТекущаяДата() - 3600 * 24);

ЮТест.Данные().ЗагрузитьИзМакетаВТаблицу(
    ИмяТаблицы, 
    Макет,
    ОбщиеРеквизиты,
    ПараметрыЗагрузкиМакетов);
```

## Другие методы загрузки

* [`ЮТест.Данные().ТаблицаMarkDown`](/api/ЮТТестовыеДанные#таблицаmarkdown) - Читает таблицу MarkDown в массив структур

```bsl
    Макет = 
    "| Имя                    | ИмяКоллекции            | Конструктор          | Группы | Ссылочный | Реквизиты | Измерения | Ресурсы | РеквизитыАдресации | ТабличныеЧасти |
    ||------------------------|-------------------------|----------------------|--------|-----------|-----------|-----------|---------|--------------------|----------------|
    || Справочник             | Справочники             | СоздатьЭлемент       | +      | +         | +         |           |         |                    | +              |
    || Документ               | Документы               | СоздатьДокумент      |        | +         | +         |           |         |                    | +              |
    || ПланВидовХарактеристик | ПланыВидовХарактеристик | СоздатьЭлемент       | +      | +         | +         |           |         |                    | +              |
    || ПланСчетов             | ПланыСчетов             | СоздатьСчет          |        | +         | +         |           |         |                    | +              |
    || ПланВидовРасчета       | ПланыВидовРасчета       | СоздатьВидРасчета    |        | +         | +         |           |         |                    | +              |
    || ПланОбмена             | ПланыОбмена             | СоздатьУзел          |        | +         | +         |           |         |                    | +              |
    || РегистрСведений        | РегистрыСведений        | СоздатьНаборЗаписей  |        |           | +         | +         | +       |                    |                |
    || РегистрНакопления      | РегистрыНакопления      | СоздатьНаборЗаписей  |        |           | +         | +         | +       |                    |                |
    || РегистрБухгалтерии     | РегистрыБухгалтерии     | СоздатьНаборЗаписей  |        |           | +         | +         | +       |                    |                |
    || РегистрРасчета         | РегистрыРасчета         | СоздатьНаборЗаписей  |        |           | +         | +         | +       |                    |                |
    || БизнесПроцесс          | БизнесПроцессы          | СоздатьБизнесПроцесс |        | +         | +         |           |         |                    | +              |
    || Задача                 | Задачи                  | СоздатьЗадачу        |        | +         | +         |           |         | +                  | +              |
    |";
    КоллекцияОписаний = ЮТТестовыеДанные.ТаблицаMarkDown(Макет);

    ТипыМетаданных = Новый Структура();

    Для Каждого Запись Из КоллекцияОписаний Цикл
        
        Описание = Новый Структура();
        Описание.Вставить("Имя", Запись.Имя);
        Описание.Вставить("ИмяКоллекции", Запись.ИмяКоллекции);
        Описание.Вставить("Конструктор", Запись.Конструктор);
        Описание.Вставить("Группы", Запись.Группы = "+");
        Описание.Вставить("Ссылочный", Запись.Ссылочный = "+");
        Описание.Вставить("Реквизиты", Запись.Реквизиты = "+");
        Описание.Вставить("Измерения", Запись.Измерения = "+");
        Описание.Вставить("Ресурсы", Запись.Ресурсы = "+");
        Описание.Вставить("РеквизитыАдресации", Запись.РеквизитыАдресации = "+");
        Описание.Вставить("ТабличныеЧасти", Запись.ТабличныеЧасти = "+");
        
        ТипыМетаданных.Вставить(Описание.Имя, Описание);
        ТипыМетаданных.Вставить(Описание.ИмяКоллекции, Описание);
        
    КонецЦикла;

    Возврат ТипыМетаданных;
```

* [`ЮТест.Данные().СтруктураMarkDown`](/api/ЮТТестовыеДанные#структураmarkdown) - Читает таблицу MarkDown в структуру

```bsl
    Функция ИмяКоллекции(Имя) Экспорт
        Макет = 
        "| Имя                    | ИмяКоллекции            |
        ||------------------------|-------------------------|
        || Справочник             | Справочники             |
        || Документ               | Документы               |
        || ПланВидовХарактеристик | ПланыВидовХарактеристик |
        || ПланСчетов             | ПланыСчетов             |
        || ПланВидовРасчета       | ПланыВидовРасчета       |
        || ПланОбмена             | ПланыОбмена             |
        || РегистрСведений        | РегистрыСведений        |
        || РегистрНакопления      | РегистрыНакопления      |
        || РегистрБухгалтерии     | РегистрыБухгалтерии     |
        || РегистрРасчета         | РегистрыРасчета         |
        || БизнесПроцесс          | БизнесПроцессы          |
        || Задача                 | Задачи                  |
        |";
        Описания = ЮТТестовыеДанные.СтруктураMarkDown("Имя", Макет);

        Возврат Описания[Имя].ИмяКоллекции;

    КонецФункции
```

