---
tags: [Начало, Тестовые данные]
sidebar_position: 1
---

# Программное создание тестовых данных

Доступ к методам генерации тестовых реализован через [`ЮТест.Данные()`](/api/ЮТест#данные), этот метод возвращает общий модуль [`ЮТТестовыеДанные`](/api/ЮТТестовыеДанные).

:::tip
Не рекомендуется обращаться к модулю `ЮТТестовыеДанные` напрямую, используйте `ЮТест.Данные()`
:::

## Возможности

Инструменты работы с тестовыми данными можно разделить на следующие группы:

* Генераторы случайных значений
* Методы создания данных в информационной базе
* Методы загрузки из макетов
* Работа с файлами

### Генераторы случайных данных

Как уже говорилось в [статье](test-data.md) в YAxUnit сделан большой упор на генерацию случайных тестовых данных, которая позволяет улучшить покрытие и сократить количество "лишнего" кода. Поставив в самом начале на такой подход мы еще ни разу в нем не разочаровались.

Создание случайных данных базируется на генерации примитивных значения:

* Числа
  * [`ЮТест.Данные().СлучайноеЧисло`](/api/ЮТТестовыеДанные#случайноечисло) - Формирует случайное число в указанном диапазоне
  * [`ЮТест.Данные().СлучайноеПоложительноеЧисло`](/api/ЮТТестовыеДанные#случайноеположительноечисло) - Формирует случайное положительное число ограниченное максимумом
  * [`ЮТест.Данные().СлучайноеОтрицательноеЧисло`](/api/ЮТТестовыеДанные#случайноеотрицательноечисло) - Формирует случайное отрицательное число ограниченное минимумом
* Строки
  * [`ЮТест.Данные().СлучайнаяСтрока`](/api/ЮТТестовыеДанные#случайнаястрока) - Формирует случайную строку указанной длины
  * [`ЮТест.Данные().СлучайныйИдентификатор`](/api/ЮТТестовыеДанные#случайныйидентификатор) - Формирует случайный валидный идентификатор (Первым символом должна быть буква или символ подчеркивания, каждый из последующих символов может быть буквой, цифрой или символом подчеркивания)
  * [`ЮТест.Данные().СлучайныйIPАдрес`](/api/ЮТТестовыеДанные#случайныйipадрес) - Формирует случайный IP адрес
  * [`ЮТест.Данные().СлучайныйНомерТелефона`](/api/ЮТТестовыеДанные#случайныйномертелефона) - Формирует случайный номер телефона
  * [`ЮТест.Данные().УникальнаяСтрока`](/api/ЮТТестовыеДанные#уникальнаястрока) - Создает строку на базе нового уникального идентификатора
* Даты
  * [`ЮТест.Данные().СлучайнаяДата`](/api/ЮТТестовыеДанные#случайнаядата) - Формирует случайную дату в указанном диапазоне
  * [`ЮТест.Данные().СлучайноеВремя`](/api/ЮТТестовыеДанные#случайноевремя) - Формирует случайное время
  * [`ЮТест.Данные().СлучайнаяДатаВБудущем`](/api/ЮТТестовыеДанные#случайнаядатавбудущем) - Формирует случайную дату в будущем (больше текущей) с возможностью ограничить максимальное значение.  
    Пример: `СлучайнаяДатаВБудущем(2, "часа")` - будет сформирована дата в интервале (ТекущаяДата, ТекущаяДата + 2 часа]
  * [`ЮТест.Данные().СлучайнаяДатаВПрошлом`](/api/ЮТТестовыеДанные#случайнаядатавпрошлом) - Формирует случайную дату в прошлом (меньше текущей) с возможностью ограничить минимальное значение.  
    Пример: `СлучайнаяДатаВПрошлом(2, "часа")` - будет сформирована дата в интервале [ТекущаяДата - 2 часа, ТекущаяДата)
  * [`ЮТест.Данные().СлучайнаяДатаПосле`](/api/ЮТТестовыеДанные#случайнаядатапосле) - Формирует случайную дату, которая больше указанной с возможностью ограничить максимальное значение.  
    Пример: `СлучайнаяДатаПосле(Дата, 2, "часа")` - будет сформирована дата в интервале (Дата, Дата + 2 часа]
  * [`ЮТест.Данные().СлучайнаяДатаДо`](/api/ЮТТестовыеДанные#случайнаядатадо) - Формирует случайную дату, которая меньше указанной с возможностью ограничить минимальное значение.  
    Пример: `СлучайнаяДатаДо(Дата, 2, "часа")` - будет сформирована дата в интервале [Дата - 2 часа, Дата)
* Булево
  * [`ЮТест.Данные().СлучайноеБулево`](/api/ЮТТестовыеДанные#случайноебулево) - Возвращает случайное булево
* Получение случайного из предопределенного списка
  * [`ЮТест.Данные().СлучайноеЗначениеИзСписка`](/api/ЮТТестовыеДанные#случайноезначениеизсписка) - Возвращает случайное значение из указанного списка

    ```bsl
    ВозможныеЗначения = ЮТКоллекции.ЗначениеВМассиве(-1, 0, 1);
    Возврат ЮТест.Данные().СлучайноеЗначениеИзСписка(ВозможныеЗначения);
    ```

  * [`ЮТест.Данные().СлучайноеЗначениеПеречисления`](/api/ЮТТестовыеДанные#случайноезначениеперечисления) - Возвращает случайное значение перечисления
    Примеры:

    ```bsl
    Цена = ЮТест.Данные().СлучайноеЗначениеПеречисления(Перечисления.ВидыЦен); // Можно получить значение через указание менеджера
    Цена = ЮТест.Данные().СлучайноеЗначениеПеречисления("Перечисление.ВидыЦен"); // Или имя объекта метаданных (подходит для работы с клиента)
    ```

  * [`ЮТест.Данные().СлучайноеПредопределенноеЗначение`](/api/ЮТТестовыеДанные#случайноепредопределенноезначение) - Возвращает случайное предопределенное значение
    Примеры:

    ```bsl
    Цена = ЮТест.Данные().СлучайноеПредопределенноеЗначение(Справочники.ВидыЦен); // Можно получить значение через указание менеджера
    Цена = ЮТест.Данные().СлучайноеПредопределенноеЗначение("Справочник.ВидыЦен"); // Или имя объекта метаданных (подходит для работы с клиента)
    ```

* [`ЮТест.Данные().Подражатель`](/api/ЮТТестовыеДанные#подражатель) - генератор правдоподобных данных. Генерирует случайные данные на базе словарей, является реализацией [faker](https://github.com/faker-js/faker) для 1С.
  * [`ЮТест.Данные().Подражатель().Люди`](/api/ЮТПодражатель#люди)
    * [`ФИО`](/api/ЮТПодражатель_Люди#фио) - Формирует случайное ФИО
    * [`Фамилия`](/api/ЮТПодражатель_Люди#фамилия) - Формирует случайную фамилию
    * [`Имя`](/api/ЮТПодражатель_Люди#имя) - Формирует случайное имя
    * [`Отчество`](/api/ЮТПодражатель_Люди#отчество) - Формирует случайное отчество
    * [`ИНН`](/api/ЮТПодражатель_Люди#инн) - Формирует случайный ИНН
    * [`СНИЛС`](/api/ЮТПодражатель_Люди#снилс) - Формирует случайный СНИЛС
  * [`ЮТест.Данные().Подражатель().Компании`](/api/ЮТПодражатель#компании)
    * [`Наименование`](/api/ЮТПодражатель_Компании#наименование) - Формирует случайное наименование компании
    * [`ИНН`](/api/ЮТПодражатель_Компании#инн) - Формирует случайный ИНН компании
    * [`КПП`](/api/ЮТПодражатель_Компании#кпп) - Формирует случайный КПП компании
  * [`ЮТест.Данные().Подражатель().Банки`](/api/ЮТПодражатель#банки)
    * [`НомерСчета`](/api/ЮТПодражатель_Банки#номерсчета) - Формирует случайный номер банковского счета
    * [`БИК`](/api/ЮТПодражатель_Банки#бик) - Формирует случайный валидный БИК банка

### Методы создания данных в информационной базе

* Создание пустышек - примитивных объектов с минимальным заполнения. Полезно использовать когда вам нужна ссылка определенного типа, но не важно ее заполнение
  * [`ЮТест.Данные().СоздатьЭлемент`](/api/ЮТТестовыеДанные#создатьэлемент) - Создает новый элемент справочника или плана видов характеристик

    ```bsl
    Товар = ЮТест.Данные().СоздатьЭлемент(Справочники.Товары); // На основании менеджера
    Товар = ЮТест.Данные().СоздатьЭлемент("Справочники.Товары"); // На основании имени менеджера
    
    ПараметрыЗаписи = ЮТФабрика.ПараметрыЗаписи();
    ПараметрыЗаписи.ОбменДаннымиЗагрузка = Истина;
    Товар = ЮТест.Данные().СоздатьЭлемент("Справочники.Товары", , , ПараметрыЗаписи); // Запись в режиме ОбменДанными.Загрузка = Истина
    ```

  * [`ЮТест.Данные().СоздатьДокумент`](/api/ЮТТестовыеДанные#создатьдокумент) - Создает новый документ

    ```bsl
    Товар = ЮТест.Данные().СоздатьДокумент(Документ.ПриходТовара); // На основании менеджера
    Товар = ЮТест.Данные().СоздатьДокумент("Документ.ПриходТовара"); // На основании имени менеджера
    ```

  * [`ЮТест.Данные().СоздатьГруппу`](/api/ЮТТестовыеДанные#создатьгруппу) - Создает новую группу справочника или плана видов характеристик

    ```bsl
    Товар = ЮТест.Данные().СоздатьГруппу(Документ.ПриходТовара); // На основании менеджера
    Товар = ЮТест.Данные().СоздатьГруппу("Документ.ПриходТовара"); // На основании имени менеджера
    ```

  * [`ЮТест.Данные().Фикция`](/api/ЮТТестовыеДанные#фикция) - Создает новый объект на основании его типа

    ```bsl
    Товар = ЮТест.Данные().Фикция(Тип("ДокументСсылка.ПриходТовара"));
    Товар = ЮТест.Данные().Фикция(Новый ОписаниеТипов("ДокументСсылка.ПриходТовара"));
    Товар = ЮТест.Данные().Фикция(Новый ОписаниеТипов("ДокументСсылка.ПриходТовара, ДокументСсылка.РасходТовара")); // Будет выбран случайный тип
    ```

* [`ЮТест.Данные().КонструкторОбъекта`](/api/ЮТТестовыеДанные#конструкторобъекта), с помощью которого вы можете создать объект информационной базы с необходимыми реквизитами.
  Конструктор позволяет:
  * В более компактной форме заполнять объект
  * Имеет методы записи и проведения
  * При возникновении ошибки оборачивает их в более понятные человеку описания
  * Киллер фича - `Фикция` и `ФикцияОбязательныхПолей`. Устанавливает случайно сгенерированные значения реквизитов. Таким образом делается акцент на важных для теста реквизитах и экономит "строки" на генерацию ненужных значений
  * Умеет создавать данные с клиента и [удалять их по окончании теста](test-data-deletion.md)
  
  ```bsl
  Конструктор = ЮТест.Данные().КонструкторОбъекта("Документы.ПриходТовара")
      .Фикция("Поставщик")
      .Фикция("Склад")
      .ФикцияОбязательныхПолей()
      .ТабличнаяЧасть("Товары");
  
  ОбщиеДанные = Новый Структура("ВидЦены, Поставщик", Закупочная, Конструктор.ДанныеОбъекта().Поставщик);

  Для Инд = 1 По 3 Цикл
      Конструктор.ДобавитьСтроку()
          .Фикция("Товар")
          .УстановитьРеквизиты(ОбщиеДанные)
          .Установить("Цена", ЮТест.Данные().СлучайноеПоложительноеЧисло(9999, 2))
          .Установить("Количество", ЮТест.Данные().СлучайноеПоложительноеЧисло(20))
          .Установить("Сумма", Конструктор.ДанныеСтроки().Цена * Конструктор.ДанныеСтроки().Количество)
  КонецЦикла;

  Возврат Конструктор.Провести();
  ```

### Загрузка данных из макетов

Работа с макетами описана в отдельной [статье](load-from-templates.md)

### Работа с файлами

* [`ЮТест.Данные().НовыйФайл`](/api/ЮТТестовыеДанные#новыйфайл) - Создает новый файл, который будет удален после теста
* [`ЮТест.Данные().НовоеИмяВременногоФайла`](/api/ЮТТестовыеДанные#новоеимявременногофайла) - Возвращает имя нового файла, по окончании выполнения теста этот файл будет удален

### Создание XDTO

При тестировании обменов или интеграций использующих SOAP вам может пригодится [`ЮТест.Данные().КонструкторОбъектаXDTO`](/api/ЮТТестовыеДанные#конструкторобъектаxdto), с помощью которого вы можете с легкостью создать XDTO объект.

```bsl
Тип = СериализаторXDTO.XMLТип(Тип("ДокументОбъект.Заказ"));

Дата = ЮТест.Данные().СлучайнаяДата();
Номер = ЮТест.Данные().СлучайнаяСтрока(9);
Сумма = ЮТест.Данные().СлучайноеЧисло();
Реквизиты = Новый Структура("Number, Сумма", Номер, Сумма);

Объект = ЮТест.Данные().КонструкторОбъектаXDTO(Тип.ИмяТипа, Тип.URIПространстваИмен)
  .Установить("Date", Дата)
  .ФикцияОбязательныхПолей()
  .УстановитьРеквизиты(Реквизиты)
  .Фикция("Покупатель")
  .ДобавитьНовый("Товары")
    .Фикция("Цена")
    .Фикция("Количество")
  .ДанныеОбъекта();
```

Он был создан на основании уже проверенного и зарекомендовавшего себя `ЮТест.Данные().КонструкторОбъекта` и обладает схожим API.

* `Установить(ИмяРеквизита, Значение)` - Устанавливает значение указанного реквизита объекта.
* `УстановитьРеквизиты(ЗначенияРеквизитов)` - Устанавливает значения реквизитов объекта.

* `Фикция(ИмяРеквизита)` - на основании типа создаваемого объекта определяет тип реквизита и устанавливает для него случайное значение.
  * Для примитивных значение генерируется случайное значение
  * Для объектных типов создается новый объект
  * Для коллекций - генерируется случайно количество случайных элементов (на основании типа)
* `ФикцияОбязательныхПолей()` - на основании типа создаваемого объекта определяет список обязательных полей и для каждого из них вызывается метод `Фикция`.
* `ФикцияНезаполненных()` - для всех незаполненных полей создаваемого объекта вызывает метод `Фикция`.
* `ДобавитьНовый(ИмяРеквизита)` - Добавляет новый объект в указанную реквизит-коллекцию. Все последующие вызовы установки реквизитов будут относится к этому добавленному объекту.
* `ПерейтиКВладельцу()` - используется после метода `ДобавитьНовый` для переключения контекста заполнения на родительский объект. Например вам необходимо добавить две строки в табличную часть.
  ```bsl
  Объект = ЮТест.Данные().КонструкторОбъектаXDTO(Тип.ИмяТипа, Тип.URIПространстваИмен)
    .ДобавитьНовый("Товары")
      .Фикция("Цена")
      .Фикция("Количество")
      .ПерейтиКВладельцу()
    .ДобавитьНовый("Товары")
      .Фикция("Цена")
      .Фикция("Количество")
    .ДанныеОбъекта();  
  ```
  Здесь перед добавлением второй строки нужно переключится (`ПерейтиКВладельцу`) на основной объект и после этого можно добавить строку.
* `ДанныеОбъекта()` - возвращает созданный объект XDTO.

## Примеры

### Генерация фейкового документа

```bsl
#Если Сервер Тогда
    Конструктор = ЮТест.Данные().КонструкторОбъекта(Документы.ПриходТовара);
#Иначе
    Конструктор = ЮТест.Данные().КонструкторОбъекта("Документы.ПриходТовара");
#КонецЕсли

    Конструктор
        .Фикция("Поставщик")
        .Фикция("Склад")
        .Фикция("Валюта")
        .Установить("Организация", ЮТест.Данные().КонструкторОбъекта("Справочники.Организации").Установить("КакойТоТамУчет", Истина))
        .ТабличнаяЧасть("Товары");
    
    Для Инд1 = 1 По ЮТест.Данные().СлучайноеЧисло(1, 5) Цикл
        Конструктор.ДобавитьСтроку()
            .Фикция("Товар")
            .Установить("Цена", ЮТест.Данные().СлучайноеПоложительноеЧисло(9999, 2))
            .Установить("Количество", ЮТест.Данные().СлучайноеПоложительноеЧисло(20))
            .Установить("Сумма", Конструктор.ДанныеСтроки().Цена * Конструктор.ДанныеСтроки().Количество)
    КонецЦикла;

    Ссылка = Конструктор.Провести();
```

### Генерация произвольных фейковых данных

```bsl
Функция ОписаниеСервера(Знач Идентификатор = Неопределено) Экспорт
    
    Описание = БазовоеОписаниеОбъекта(Идентификатор, "СРВ");
    
    Описание.Вставить("Адрес", ЮТТестовыеДанные.СлучайныйIPАдрес());
    Описание.Вставить("ЧастотаЯдра", ЮТТестовыеДанные.СлучайноеПоложительноеЧисло(4, 3));
    Описание.Вставить("КоличествоЯдер", ЮТТестовыеДанные.СлучайноеПоложительноеЧисло(10));
    Описание.Вставить("КоличествоПотоков", Описание.КоличествоЯдер * 2);
    Описание.Вставить("ПроизводительностьПроцессора", Описание.ЧастотаЯдра * Описание.КоличествоЯдер);
    Описание.Вставить("ОбъемПамяти", ЮТТестовыеДанные.СлучайноеПоложительноеЧисло(1024, 3));
    Описание.Вставить("Диски", Новый Массив());
    Описание.Вставить("Кластер");
    
    Для Инд = 1 По ЮТТестовыеДанные.СлучайноеЧисло(1, 3) Цикл
        Описание.Диски.Добавить(ОписаниеДиска());
    КонецЦикла;
    
    Возврат Описание;
    
КонецФункции

Функция ОписаниеЗаявкиНаСозданиеВМ() Экспорт
    
    Описание = Новый Структура();
    
    Описание.Вставить("Наименование",         "req-" + ЮТТестовыеДанные.СлучайнаяСтрока());
    Описание.Вставить("ДатаДобавления",       глПолучитьМосковскоеВремя());
    Описание.Вставить("ДатаОкончанияАренды",  глДобавитьКДате(глПолучитьМосковскоеВремя(), "ДЕНЬ", 2));
    Описание.Вставить("Адрес",                ЮТТестовыеДанные.СлучайныйIPАдрес());
    
    Описание.Вставить("Проект",               Проект());
    Описание.Вставить("Ментейнер",           ПараметрыСеанса.Сотрудник);
    
    Описание.Вставить("КоличествоCPU",        ЮТТестовыеДанные.СлучайноеЧисло(1, 4));
    Описание.Вставить("КоличествоПамяти",     ЮТТестовыеДанные.СлучайноеЧисло(1, 16));
    Описание.Вставить("КоличествоДисков",     ЮТТестовыеДанные.СлучайноеЧисло(1, 16));
    Описание.Вставить("ОперационнаяСистема",  ЮТТестовыеДанные.СоздатьЭлемент(Справочники.ОперационныеСистемы));
    
    Описание.Вставить("СредаЭксплуатации",    "");
    Описание.Вставить("Назначение",           ЮТТестовыеДанные.СоздатьЭлемент(Справочники.НазначенияВиртуальныхМашин));
    Описание.Вставить("ТипСреды",             Справочники.ТипыИнформационныхСред.BETA);
    Описание.Вставить("КодСервиса",           ЮТТестовыеДанные.СлучайнаяСтрока(1));
    
    Возврат Описание;
    
КонецФункции

Функция НовыйОбразDocker(Версия, ПоУмолчанию = Истина) Экспорт
    
    Данные = Новый Структура("Адрес, ВерсияПлатформы, ТипПлатформы, ИспользоватьПоУмолчанию");
    Данные.Адрес = "gitlab.ru/orais/ci_cd/1cws-apache24:" + Версия;
    Данные.ВерсияПлатформы = Версия;
    Данные.ТипПлатформы = Перечисления.ТипыСоединенияИсточникаДанных.Соединение1СПредприятие83Сервер;
    Данные.ИспользоватьПоУмолчанию = ПоУмолчанию;
    
    Возврат ЮТТестовыеДанные.СоздатьЭлемент(Справочники.ОбразыDocker, Версия, Данные);
    
КонецФункции
```
