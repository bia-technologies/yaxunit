---
tags: [Начало, Утверждения]
---

# Базовые утверждения

Утверждения для проверки значений

Доступ к утверждениям обеспечивает метод [`ЮТест.ОжидаетЧто`](/api/ЮТест#ожидаетчто), который возвращает инициализированный модуль [`ЮТУтверждения`](/api/ЮТУтверждения), реализующий работу с утверждениями.

:::tip
Не рекомендуется обращаться к модулю `ЮТУтверждения` напрямую, используйте `ЮТест.ОжидаетЧто`
:::

* Реализован достаточный набор проверок значений
  * [Сравнение значений](#сравнение-значений)
  * [Методы позиционирования](#методы-позиционирования)
  * [Проверка заполненности](#проверка-заполненности)
  * [Проверка строк](#проверка-строк)
  * [Проверка вхождения значения в интервал](#проверка-вхождения-значения-в-интервал)
  * [Проверка типа значения](#проверка-типа-значения)
  * [Проверка выполнения метода](#проверка-выполнения-метода)
  * [Проверка наличия свойств/реквизитов](#проверка-наличия-свойствреквизитов)
  * [Проверка коллекции](#проверка-коллекции)
  * [Проверка на соответствие набору условий, предикату](#проверка-на-соответствие-набору-условий-предикату)
* Большая часть методов - это сравнения фактического и ожидаемого результатов, но есть несколько методов настройки
  * [`Что`](/api/ЮТУтверждения#что) - устанавливает проверяемый объект. Все дальнейшие проверки будут выполняется с этим объектом
  * [`Метод`](/api/ЮТУтверждения#метод) - устанавливает имя и параметры проверяемого метода. Для проверки методов имеются утверждения [`ВыбрасываетИсключение`](/api/ЮТУтверждения#выбрасываетисключение) и [`НеВыбрасываетИсключение`](/api/ЮТУтверждения#невыбрасываетисключение)
  * [`Параметр`](/api/ЮТУтверждения#параметр) - добавляет параметр метода. Создан для удобства установки параметров проверяемого метода
  * [`Свойство`](/api/ЮТУтверждения#свойство) - устанавливает проверяемое свойство и проверяет его наличие.
    * Последующие проверки, вызванные после этого метода, будут относиться к свойству объекта.
      Например, `ЮТест.ОжидаетЧто(Контекст).Свойство("ИмяМетода").Равно("МетодБезИсключение")` эквивалентно проверке `Контекст.ИмяМетода = "МетодБезИсключение"`
* Методы работы со свойствами позволяют указывать цепочку свойств (доступ к вложенным свойствам через точку)
  Например:
  * `Свойство("Контекст.ИмяМетода")` - вложенное свойство
  * `ИмеетСвойство("Контекст.ПараметрыМетода[0]")` - элемент вложенной коллекции
  * `НеИмеетСвойства("[0].Свойство")` - свойство элемента коллекции
* Все методы имеют параметр `ОписаниеПроверки` для детального описания.

## Доступные методы

:::tip
Полный и актуальный набор методов смотрите в описании API
:::

### Сравнение значений

* [`Равно`](/api/ЮТУтверждения#равно) - проверка на равенство конкретному значению. Для сериализуемых объектов идет сравнение по значению
* [`НеРавно`](/api/ЮТУтверждения#неравно) - проверка на не равенство конкретному значению. Для сериализуемых объектов идет сравнение по значению
* [`Больше`](/api/ЮТУтверждения#больше) - проверяемое значение должно быть больше указанного
* [`БольшеИлиРавно`](/api/ЮТУтверждения#большеилиравно) - проверяемое значение должно быть больше или равно указанному
* [`Меньше`](/api/ЮТУтверждения#меньше) - проверяемое значение должно быть меньше указанного
* [`МеньшеИлиРавно`](/api/ЮТУтверждения#меньшеилиравно) - проверяемое значение должно быть меньше или равно указанному
* [`ЭтоНеопределено`](/api/ЮТУтверждения#этонеопределено) - проверяемое значение должно быть равно `Неопределено`
* [`ЭтоНеНеопределено`](/api/ЮТУтверждения#этоненеопределено) - проверяемое значение должно быть не равно `Неопределено`
* [`ЭтоNull`](/api/ЮТУтверждения#этоnull) - проверяемое значение должно быть равно `Null`
* [`ЭтоНеNull`](/api/ЮТУтверждения#этонеnull) - проверяемое значение должно быть не равно `Null`
* [`ЭтоИстина`](/api/ЮТУтверждения#этоистина) - проверяемое значение должно быть истиной
* [`ЭтоНеИстина`](/api/ЮТУтверждения#этонеистина) - проверяемое значение не должно быть истиной
* [`ЭтоЛожь`](/api/ЮТУтверждения#этоложь) - проверяемое значение должно быть ложью
* [`ЭтоНеЛожь`](/api/ЮТУтверждения#этонеложь) - проверяемое значение не должно быть ложью
* [`ВСписке`](/api/ЮТУтверждения#этонеложь) - проверяемое значение должно входить в список указанных.

```bsl
Значение = 1;
ЮТУтверждения.Что(Значение)
  .Равно(1)
  .НеРавно(2)
  .Больше(0)
  .БольшеИлиРавно(-10)
  .Меньше(10)
  .МеньшеИлиРавно(2)
  .ЭтоНеИстина()
  .ЭтоНеЛожь()
  .ВСписке(ЮТКоллекция.ЗначениеВМассиве(0, 1, 2));
```

#### Особенности сравнения

* При сравнении значений сравниваются их типы. Это отличается от стандартного поведения 1С:Предприятие, где некоторые значения разных типов могут быть равны, например `1 = Истина`
  ```bsl
  ЮТест.ОжидаетЧто(1 = Истина).ЭтоИстина(); // Проверка успешна
  ЮТест.ОжидаетЧто(1).Равно(Истина); // Проверка провалится
  ```
* Для большинства типов данных реализовано сравнение "по значению", когда сравниваются (рекурсивно) значения реквизитов объекта.
* Поддерживается сравнение структур и соответствий.
  ```bsl
  Значение1 = Новый Структура("Реквизит", 1);
  Значение2 = Новый Структура("Реквизит", 1);
  ЮТест.ОжидаетЧто(Значение1 = Значение2).ЭтоИстина(); // Проверка провалится
  ЮТест.ОжидаетЧто(Значение1).Равно(Значение2); // Проверка успешна
  ```
* Поддерживается сравнение массивов, таблиц и табличных документов.
* Поддерживается сравнение значений упакованных в `ХранилищеЗначения`.
* Для прочих сериализуемых значений (не примитивов) сравнение происходит через сериализацию в JSON.

### Методы позиционирования

Любое из утверждений можно применять как к самому анализируемому объекту, так и к его свойствам, реквизитам, вложенным элементам.  
Например, проверить, наличие заполненной табличной части документа используя выражение 

```bsl
ЮТест.ОжидаетЧто(Документ)
  .Свойство("Товары").Заполнено()
  .Свойство("Товары[0].Номенклатура").Равно(ОжидаемыйТовар)
;
```

Благодаря методам [`Свойство`](/api/ЮТУтверждения#свойство) и [`Элемент`](/api/ЮТУтверждения#элемент) можно позиционировать (переключать объект проверки) утверждения на вложенный реквизит/элемент.

Методы [`Свойство`](/api/ЮТУтверждения#свойство) и [`Элемент`](/api/ЮТУтверждения#элемент) переключают объект проверки для последующих методов-утверждений. Они будут применяться к значению свойства или элементу коллекции. Но не переключает сам контекст на это значение, а всегда применяется к исходному объекту, помещенному в [`ОжидаетЧто`](/api/ЮТест#ожидаетчто).

* [`Свойство`](/api/ЮТУтверждения#свойство) - проверяет наличие свойства и позиционирует дальнейшие проверки на указанном свойстве.
* [`Элемент`](/api/ЮТУтверждения#элемент) - проверяет наличие элемента коллекции и позиционирует дальнейшие проверки на указанном элементе. На самом деле это просто алиас (псевдоним) для метода `Свойство` и выполняет он тоже самое.
* [`Объект`](/api/ЮТУтверждения#объект) - позиционирует дальнейшие проверки на объекте, указанном в методе [`ОжидаетЧто`](/api/ЮТест#ожидаетчто).
* [`НетСвойства`](/api/ЮТУтверждения#нетсвойства) - проверяет отсутствие свойства и позиционирует дальнейшие проверки на объекте, указанном в методе [`ОжидаетЧто`](/api/ЮТест#ожидаетчто).

:::caution
Методы [`Свойство`](/api/ЮТУтверждения#свойство) и [`Элемент`](/api/ЮТУтверждения#элемент) всегда вычисляют путь от объекта указанного в [`ОжидаетЧто`](/api/ЮТест#ожидаетчто), они не учитывают предыдущие изменения позиции.

Например, чтобы проверить значение реквизита `Реквизит.ВложенныйРеквизит`

```bsl title="Неправильно"
Объект = Новый Структура("Реквизит", Новый Структура("ВложенныйРеквизит", 1));

ЮТест.ОжидаетЧто(Объект)
  .Свойство("Реквизит")
    .Свойство("ВложенныйРеквизит").Равно(1) // Выбросит исключение, тк объект не содержит свойство "ВложенныйРеквизит"
;
```

Правильно в таком примере указывать сразу полный путь для свойства.

```bsl title="Правильно"
ЮТест.ОжидаетЧто(Объект)
  .Свойство("Реквизит.ВложенныйРеквизит").Равно(1)
;
```

:::

Методы [`Свойство`](/api/ЮТУтверждения#свойство) и [`Элемент`](/api/ЮТУтверждения#элемент), принимают на вход не просто имя реквизита, а выражение, для перехода к нужному свойству, таким образом вы можете позиционироваться:

* На свойствах объекта
  ```bsl
  ЮТест.ОжидаетЧто(Объект)
    .Свойство("Реквизит").Равно(ОжидаемоеЗначение)
  ```
* На вложенных свойствах объекта `Свойство("Реквизит.ВложенныйРеквизит")`
  ```bsl
  ЮТест.ОжидаетЧто(Объект)
    .Свойство("Реквизит.ВложенныйРеквизит").Равно(ОжидаемоеЗначение)
  ```
* На элементах коллекций
  ```bsl
  ЮТест.ОжидаетЧто(Документ.Товары)
    .Свойство("[0].Товар").Равно(ОжидаемоеЗначение);
  // Или
  ЮТест.ОжидаетЧто(Документ)
    .Свойство("Товары[0].Товар").Равно(ОжидаемоеЗначение);
  ```
* Можно позиционировать на элементах коллекции с конца  
  ```bsl
  ЮТест.ОжидаетЧто(Документ.Товары)
    .Свойство("[-1].Товар").Равно(ОжидаемоеЗначение) // Последний элемент;
    .Элемент(-1).ИмеетСвойство("Товар") // Последний элемент
  ```

### Проверка заполненности

* [`Заполнено`](/api/ЮТУтверждения#заполнено) - проверяет заполненность значения
  ```bsl
  ЮТест.ОжидаетЧто(ОбъектПроверки)
    .Заполнено()
    .Свойство("Номер").Заполнено();
  ```
* [`НеЗаполнено`](/api/ЮТУтверждения#незаполнено) - проверяет незаполненность значения
  ```bsl
  Значения = ЮТКоллекции.ЗначениеВМассиве(0, "", " ", Неопределено, Null, '00010101', Новый Структура(), Справочники.Товары.ПустаяСсылка());
  Для каждого Значение Из Значения Цикл
    ЮТест.ОжидаетЧто(Значение)
      .НеЗаполнено()
  КонецЦикла;
  ```
* [`Существует`](/api/ЮТУтверждения#существует) - проверяет существование (не равно `Null` и `Неопределено`) значения
  ```bsl
  ЮТест.ОжидаетЧто(ВыборкаЗапроса)
    .Свойство("Номер").Существует();
  ```
* [`НеСуществует`](/api/ЮТУтверждения#несуществует) - проверяет не существование (не равно `Null` и `Неопределено`) значения
  ```bsl
  ЮТест.ОжидаетЧто(ВыборкаЗапроса)
    .Свойство("Номер").НеСуществует();
  ```

### Проверка строк

* [`ИмеетДлину`](/api/ЮТУтверждения#имеетдлину) - проверяет, что строка имеет указанную длину
* [`ИмеетДлинуБольше`](/api/ЮТУтверждения#имеетдлинубольше) - проверяет, что длин строки больше указанной
* [`ИмеетДлинуМеньше`](/api/ЮТУтверждения#имеетдлинуменьше) - проверяет, что длина строки меньше указанной
* [`НеИмеетДлину`](/api/ЮТУтверждения#неимеетдлину) - проверяет, что длина строки отличается от указанной
* [`Содержит`](/api/ЮТУтверждения#содержит) - проверяемая строка содержит указанную подстроку
* [`НеСодержит`](/api/ЮТУтверждения#несодержит) - проверяемая строка не содержит указанную подстроку
* [`НачинаетсяС`](/api/ЮТУтверждения#начинаетсяс) - проверяемая строка начинается с указанной строки
* [`ЗаканчиваетсяНа`](/api/ЮТУтверждения#заканчиваетсяна) - проверяемая строка заканчивается на указанную строку

```bsl
ПроверяемоеЗначение = "Ох, нелегкая это работа - Из болота тащить бегемота";
ЮТест.ОжидаетЧто("ПроверяемоеЗначение")
  .ИмеетДлинуБольше(10)
  .ИмеетДлинуМеньше(100)
  .ИмеетДлину(51)
  .Содержит("работа")
  .НеСодержит("зарплата")
  .НачинаетсяС("Ох")
  .ЗаканчиваетсяНа("мота")
```

* [`СодержитСтрокуПоШаблону`](/api/ЮТУтверждения#содержитстрокупошаблону) - проверяемая строка содержит подстроку, соответствующую регулярному выражению
* [`НеСодержитСтрокуПоШаблону`](/api/ЮТУтверждения#несодержитстрокупошаблону) - проверяемая строка не содержит подстроку, соответствующую регулярному выражению

```bsl
ПроверяемоеЗначение = "Ох, нелегкая это работа - Из болота тащить бегемота";
ЮТест.ОжидаетЧто("ПроверяемоеЗначение")
  .СодержитСтрокуПоШаблону("^[а-яА-Я\s,\-]*$") // Строка содержит только кириллицу, пробелы и знаки пунктуации
  .НеСодержитСтрокуПоШаблону("\d") // Не содержит цифр
```

### Проверка вхождения значения в интервал

* [`МеждуВключаяГраницы`](/api/ЮТУтверждения#междувключаяграницы) - проверяемое значение находиться в указанному интервале (включая границы)
* [`МеждуИсключаяГраницы`](/api/ЮТУтверждения#междуисключаяграницы) - проверяемое значение находиться в указанному интервале (исключая границы)
* [`МеждуВключаяНачалоГраницы`](/api/ЮТУтверждения#междувключаяначалограницы) - проверяемое значение находиться в указанному интервале (включая левую границу и исключая правую)
* [`МеждуВключаяОкончаниеГраницы`](/api/ЮТУтверждения#междувключаяокончаниеграницы) - проверяемое значение находиться в указанному интервале (исключая левую границу и включая правую)

```bsl
ЮТест.ОжидаетЧто(10)
  .МеждуВключаяГраницы(0, 10)
  .МеждуИсключаяГраницы(0, 100);
ЮТест.ОжидаетЧто(ТекущаяДата())
  .МеждуВключаяНачалоГраницы(НачалоДня, НачалоСледующегоДня);
```

### Проверка типа значения

* [`ИмеетТип`](/api/ЮТУтверждения#имееттип) - проверяемое значение должно иметь указанный тип
* [`НеИмеетТип`](/api/ЮТУтверждения#неимееттип) - тип проверяемого значения должен отличаться от указанного

В качестве аргумента можно указывать

* Имя типа строкой или список имен типов через запятую.  
  `ИмеетТип("Строка").ИмеетТип("Строка, Число")`
* Конкретный тип.  
  `ИмеетТип(Тип("Строка"))`
* Описание типов.  
  `ИмеетТип(Новый ОписаниеТипов("Строка, Число"))`

### Проверка выполнения метода

API для проверки, выбрасываемых методом проверяемого объекта исключения.  
Первоначально необходимо описать какой метод нужно проверить, к какому объекту он относится и с какими параметрами его необходимо вызвать. Для этих целей необходимо использовать:

* [`ЮТест.ОжидаетЧто`](/api/ЮТест#ожидаетчто) - устанавливает владельца метода
* [`Метод`](/api/ЮТУтверждения#метод) - устанавливает имя и параметры проверяемого метода
* [`Параметр`](/api/ЮТУтверждения#параметр) - добавляет параметр метода. Создан для удобства установки параметров проверяемого метода

После формируем ожидание - должен или нет методы выбросить исключение и с каким текстом.

* [`ВыбрасываетИсключение`](/api/ЮТУтверждения#выбрасываетисключение) - выполняет настроенный метод объекта с параметрами и проверяет, что метод выбросит исключение и текст исключения содержит(включает) указанную в строку. При необходимости можно настроить (передать параметр), чтобы метод выполнился **в транзакции**.

  ```bsl
  ЮТест.ОжидаетЧто(Документ) // Объект, метод которого проверяем
    .Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение) // Указываем проверяемый метод и параметры вызова
    .ВыбрасываетИсключение("Значение поля ""Дата"" не может быть пустой датой") // Утверждение-проверка, исключение должно содержать указанный текст.
    .ВыбрасываетИсключение("""Дата""") // Можно указать только часть исключения, проверить вхождение указанной строки.
    .ВыбрасываетИсключение("""Дата""", , Истина) // Выполнение указанного метода в транзакции
  ```

  :::info Вопрос
  Можно ли **не указывать** текст исключения?  
  **Ответ:** Нельзя, вы должны конкретизировать свои ожидания, зафиксировать, какую ошибку ждете, иначе поведение может измениться и тест это не отработает.
  :::
* [`НеВыбрасываетИсключение`](/api/ЮТУтверждения#невыбрасываетисключение) - выполняет настроенный метод объекта с параметрами и проверяет, что метод не выбросит исключение или выбросит исключения с текстом отличным от указанного. При необходимости можно настроить (передать параметр), чтобы метод выполнился **в транзакции**.

  ```bsl
  ЮТест.ОжидаетЧто(Документ) // Объект, метод которого проверяем
    .Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение) // Указываем проверяемый метод и параметры вызова
    .НеВыбрасываетИсключение() // Утверждение-проверка, метод выполниться без ошибки.
    .НеВыбрасываетИсключение("""Дата""") // Утверждение-проверка, метод выполниться без ошибки, либо с ошибкой, которая не содержит "Дата".
    .НеВыбрасываетИсключение("""Дата""", , Истина) // Выполнение указанного метода в транзакции
  ```

  :::caution Не злоупотребляйте проверкой `НеВыбрасываетИсключение`
  Многие используют это утверждения, для вызова чуть ли не каждого проверяемого метода.
  Этого делать не стоит.  
  Это утверждение должно редко применяться, если ваш метод упадет, это и так будет отражено в отчете.
  Один из случаев когда оно может применяться, вы используете его **перед** проверками на исключения, чтобы убедится, что метод рабочий.

  ```bsl
  Документ = СоздатьДокумент();
  Утверждение = ЮТест.ОжидаетЧто(Документ)
    .Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
    .НеВыбрасываетИсключение("""Дата"""); // Проверим, что при нормальном заполнении ошибки нет

  Документ.Дата = Неопределено;
  Утверждение.ВыбрасываетИсключение("""Дата""", "Документ проведен при незаполненной дате");
  ```

  :::

  :::caution Нельзя явно вызывать проверяемый метод
  Хочу обратить внимание. Когда вы проверяете метод на исключения/их отсутствие вы должны указать:
  
  ```bsl
  ЮТест.ОжидаетЧто(Документ)                   // 1. Владельца метода
    .Метод("Записать")                         // 2. Имя метода
    .Параметр(РежимЗаписиДокумента.Проведение) // 3. Параметры вызова метода

  // Явно вызывать метод нельзя.
  ЮТест.ОжидаетЧто(Документ)
    .Метод(Документ.Записать(РежимЗаписиДокумента.Проведение)) // Так делать нельзя, так не сработает проверка.
  ```
  
  Вы **не должны** явно вызывать метод, это должен сделать тестовый движок, чтобы поймать и проанализировать ошибку.
  :::

### Проверка наличия свойств/реквизитов

* [`ИмеетСвойство`](/api/ЮТУтверждения#имеетсвойство) - проверяемый объект должен содержать указанное свойство
* [`НеИмеетСвойства`](/api/ЮТУтверждения#неимеетсвойства) - проверяемый объект не содержит указанное свойство
* [`ИмеетСвойстваРавные`](/api/ЮТУтверждения#имеетсвойстваравные) - проверяемый объект должен содержать указанный набор свойств/реквизитов и значений

Поддерживается проверка наличия свойство для различных объектов: структур, массивов, таблиц, документов и тд.  
Вы можете проверить наличие свойства не только у текущего объекта, но и у его дочерних.  
Например, `ИмеетСвойство("Родитель.Наименование")`, будет выполнена проверка наличия свойства `Родитель` у текущего объекта и свойства `Наименование` у родителя.

Также можно проверять наличие свойств у коллекций.  
Например, `ИмеетСвойство("Товары[0].Наименование")`, утверждение проверит наличие свойства `Товары`, наличие у него первого элемента и наличие свойства `Наименование` у этого элемента.

```bsl
Документ = Документы.ПриходТовара.СоздатьДокумент();

ЮТест.ОжидаетЧто(Документ)
  .ИмеетСвойство("Товары[0]")
  .Свойство("Товары[0]")
    .ИмеетСвойство("Товар")
    .ИмеетСвойство("Количество")
    .ИмеетСвойство("Цена");
```

Как вы могли заменить в качестве имени свойства указывается выражение, такое же как и обычном коде.

```bsl
Документ = Документы.ПриходТовара.СоздатьДокумент();

Свойство = Документ.Товары[0].Наименование;
ЮТест.ОжидаетЧто(Документ)
  .Свойство("Товары[0].Наименование");
// И там и там для того, чтобы добраться до наименования используется одно и тоже выражение.
```

### Проверка коллекции

* [`ИмеетДлину`](/api/ЮТУтверждения#имеетдлину) - проверяет, что коллекция имеет  указанный размер
* [`ИмеетДлинуБольше`](/api/ЮТУтверждения#имеетдлинубольше) - проверяет, что коллекция имеет размер, который больше указанного
* [`ИмеетДлинуМеньше`](/api/ЮТУтверждения#имеетдлинуменьше) - проверяет, что коллекция имеет размер, который меньше указанного
* [`НеИмеетДлину`](/api/ЮТУтверждения#неимеетдлину) - проверяет, что размер коллекции отличается от указанного
  
  ```bsl
  Коллекция = ЮТКоллекции.ЗначениеВМассиве(1, 2, 3);
  ЮТест.ОжидаетЧто(Коллекция)
    .ИмеетДлину(3)
    .ИмеетДлинуБольше(2)
    .ИмеетДлинуМеньше(4)
    .НеИмеетДлину(4)
  ;
  ```

* [`Содержит`](/api/ЮТУтверждения#содержит) - проверяемая коллекция должна содержать указанный элемент
* [`НеСодержит`](/api/ЮТУтверждения#несодержит) - проверяемая коллекция не должна содержать указанный элемент
  
  ```bsl
  Коллекция = ЮТКоллекции.ЗначениеВМассиве(1, 2, 3);
  ЮТест.ОжидаетЧто(Коллекция)
    .Содержит(1) // Конкретное значение
    .Содержит(ЮТест.Предикат() // Значение по условиям
      .ИмеетТип("Число")
      .Больше(2))
    .НеСодержит(0)
    .НеСодержит(ЮТест.Предикат()  // Не содержит строк
      .ИмеетТип("Строка"))
  ;
  ```

* [`КаждыйЭлементСодержитСвойство`](/api/ЮТУтверждения#каждыйэлементсодержитсвойство) - проверяет, что каждый элемент коллекции имеет указанное свойство
* [`КаждыйЭлементСодержитСвойствоСоЗначением`](/api/ЮТУтверждения#каждыйэлементсодержитсвойствосозначением) - проверяет, что каждый элемент коллекции имеет указанное свойство, которое равно ожидаемому значению
* [`ЛюбойЭлементСодержитСвойство`](/api/ЮТУтверждения#любойэлементсодержитсвойство) - проверяет, что в коллекции есть элемент содержащий указанное свойство
* [`ЛюбойЭлементСодержитСвойствоСоЗначением`](/api/ЮТУтверждения#любойэлементсодержитсвойствосозначением) - проверяет, что в коллекции есть элемент содержащий указанное свойство, которое равно ожидаемому значению
  
  ```bsl
  Коллекция = ЮТКоллекции.ЗначениеВМассиве(
    Новый Структура("С1", 1), 
    Новый Структура("С1, С2", 1, 2)
  );
  ЮТест.ОжидаетЧто(Коллекция)
    .КаждыйЭлементСодержитСвойство("С1")               // Проверка, что все элементы коллекции содержат свойство `С1`
    .КаждыйЭлементСодержитСвойствоСоЗначением("С1", 1) // Проверка, что все элементы коллекции содержат свойство `С1` со значением `1`
    .ЛюбойЭлементСодержитСвойство("С2")                // Проверка, что хотя бы один элемент коллекции содержат свойство `С2`
    .ЛюбойЭлементСодержитСвойство("С2", 2)             // Проверка, что хотя бы один элемент коллекции содержат свойство `С2` со значением `2`
  ;
  ```

* [`КаждыйЭлементСоответствуетПредикату`](/api/ЮТУтверждения#каждыйэлементсоответствуетпредикату) - проверяет, что элементы коллекции соответствуют переданным условиям
  
  ```bsl
  Коллекция = ЮТКоллекции.ЗначениеВМассиве(1, 2, 3);
  ЮТест.ОжидаетЧто(Коллекция)
    .КаждыйЭлементСоответствуетПредикату(ЮТест.Предикат()
      .ИмеетТип("Число") // Все элементы это числа
      .НеРавно(0))       // и каждый не равен `0`
  ```

* [`ЛюбойЭлементСоответствуетПредикату`](/api/ЮТУтверждения#любойэлементсоответствуетпредикату) - проверяет, что коллекция содержит элемент, который соответствует переданным условиям

  ```bsl
  Коллекция = ЮТКоллекции.ЗначениеВМассиве(1, 2, 3);
  ЮТест.ОжидаетЧто(Коллекция)
    .ЛюбойЭлементСоответствуетПредикату(ЮТест.Предикат()
      .ИмеетТип("Число") // Есть элемент, который является числом
      .Больше(2))        // и больше `2`
  ```

### Проверка на соответствие набору условий, предикату

* [`Содержит`](/api/ЮТУтверждения#содержит)/[`ЛюбойЭлементСоответствуетПредикату`](/api/ЮТУтверждения#любойэлементсоответствуетпредикату) - проверяемая коллекция должна содержать элемент, который соответствует переданным условиям
* [`НеСодержит`](/api/ЮТУтверждения#несодержит) - проверяемая коллекция не должна содержать элемент, который соответствует переданным условиям
* [`КаждыйЭлементСоответствуетПредикату`](/api/ЮТУтверждения#каждыйэлементсоответствуетпредикату) - проверяет, что элементы коллекции соответствуют переданным условиям
* [`СоответствуетПредикату`](/api/ЮТУтверждения#соответствуетпредикату) - проверяет, что объект или его свойство соответствует набору условий

Работа и суть предикатов описана в [отдельной статье](../predicates.md).

#### Предикаты позволяют проверить каждый элемент коллекцию на соответствие набору условий

Предположим, нам необходимо проверить, что движения документа выполненные по регистру делают приход. Тогда тест может выглядеть так.

```bsl
Документ = СоздатьИПровестиДокумент(...);
Движения = ЮТЗапросы.ДвиженияДокумента(Документ, "ОстаткиТоваров");

ЮТест.ОжидаетЧто(Движения)
  .Заполнено()
  .ИмеетДлину(Документ.Товары.Количество())
  .КаждыйЭлементСоответствуетПредикату(
    ЮТест.Предикат()
      .Реквизит("ВидДвижения").Равно(ВидДвиженияНакопления.Приход)
      .Реквизит("Количество").Больше(0)
  )
;

// Условия предиката будут применены для каждой записи движений. Если ожидания не совпадут, то будет выброшена ошибка.
```

#### Предикаты позволяют проверить наличие элемента по сложным условиям

```bsl
Документ = СоздатьИПровестиДокумент(...);
Движения = ЮТЗапросы.ДвиженияДокумента(Документ, "ОстаткиТоваров");

ЮТест.ОжидаетЧто(ПолучитьСообщенияПользователю())
  .Заполнено()
  .Содержит(
    ЮТест.Предикат()
      .Реквизит("Текст")
        .НачинаетсяС("Начало")
        .ЗаканчиваетсяНа("окончание")
        .НеСодержит("исключение")
  )
;
```

## Примеры

### Базовые проверки

Предположим, у нас имеется функция `Сложение`, которая выполняет сложение двух параметров.

Проверки для нее могут выглядеть следующим образом:

```bsl title="Тест для 'Сложение'"
Результат = Сложение(2, 3);
ЮТест.ОжидаетЧто(Результат, "Сложение чисел") // Используя модуль утверждений установим проверяемое значение и пояснение
  .ИмеетТип("Число") // Проверим тип
  .Заполнено() // Заполненность проверяемого значения
  .Больше(0) // Сравним с нулем
  .Равно(5); // Проверим ожидаемый результат

Результат = Сложение("2", 3);
ЮТест.ОжидаетЧто(Результат, "Сложение строки и числа")
  .ИмеетТип("Строка")
  .Равно("23");

Результат = Сложение('2000010101', 3);
ЮТест.ОжидаетЧто(Результат, "Сложение даты и числа")
  .Равно('2000010101000003');
```

В примере, я на каждом этапе уменьшаю количество проверок. В реальном тесте хватит проверки на равенство, остальные проверки здесь для того, чтобы показать возможности механизма.

### Проверка сложного объекта

```bsl
ЮТест.ОжидаетЧто(Ссылка, "Созданный объект")
  .Заполнено()
  .ИмеетТип("ДокументСсылка.ПриходТовара")
  .Свойство("Номер").Заполнено()
  .Свойство("Дата").Заполнено()
  .Свойство("Поставщик").Равно(Поставщик)
  .Свойство("Склад").Равно(Склад)
  .Свойство("Валюта").Равно(Валюта)
  .Свойство("Организация").Равно(Организация)
  .Свойство("Товары").ИмеетДлину(2)
  .Свойство("Товары[0].Товар").Равно(Товар1)
  .Свойство("Товары[0].Цена").Больше(0)
  .Свойство("Товары[0].Количество").Больше(0)
  .Свойство("Товары[0].Сумма").Равно(100)
  .Свойство("Товары[-1].Товар").Равно(Товар2)
  .Свойство("Товары[-1].Сумма").Равно(200)
  .Свойство("Проведен").ЭтоИстина()
```

### Проверка вызова метода

```bsl
ЮТУтверждения.Что(ОМ_ЮТУтверждения)
  .Метод("МетодБезИсключение", ЮТКоллекции.ЗначениеВМассиве("Исключение"))
  .НеВыбрасываетИсключение()
  .НеВыбрасываетИсключение("Ожидаемое исключение");
ЮТУтверждения.Что(ОМ_ЮТУтверждения)
  .Метод("МетодИсключение", ЮТКоллекции.ЗначениеВМассиве("Исключение", 2))
  .ВыбрасываетИсключение("Слишком много фактических параметров");
ЮТУтверждения.Что(ОМ_ЮТУтверждения)
  .Метод("МетодИсключение", ЮТКоллекции.ЗначениеВМассиве("Исключение"))
  .ВыбрасываетИсключение("Исключение");
ЮТУтверждения.Что(ОМ_ЮТУтверждения)
  .Метод("МетодБезИсключение")
  .ВыбрасываетИсключение("Недостаточно фактических параметров");
```

### Проверка соответствия предикату

```bsl
Дата = ЮТест.Данные().СлучайнаяДата();

Объект = Новый Структура;
Объект.Вставить("Число", 1);
Объект.Вставить("Строка", "1");
Объект.Вставить("Дата", Дата);
Объект.Вставить("Массив", ЮТКоллекции.ЗначениеВМассиве(1, "1"));

ПроверкаЧисла = ЮТест.Предикат().Реквизит("Число")
  .ИмеетТип(Тип("Число"))
  .БольшеИлиРавно(1)
  .МеньшеИлиРавно(10)
  .Получить();
ПроверкаДаты = ЮТест.Предикат().Реквизит("Дата")
  .ИмеетТип(Новый ОписаниеТипов("Дата"))
  .Равно(Дата)
  .Получить();

ЮТест.ОжидаетЧто(Объект)
  .СоответствуетПредикату(ЮТест.Предикат()
    .Заполнено()
    .ИмеетТип("Структура"))
  .СоответствуетПредикату(ПроверкаЧисла)
  .СоответствуетПредикату(ПроверкаДаты)
;
```

### Проверка элементов коллекции на соответствие предикату

```bsl
ТаблицаРезультатов = ЮТест.Данные().ЗагрузитьИзМакета("ОбщийМакет.ЮТ_МакетТестовыхДанных.R2C1:R5C11", ОписанияТипов);
ЮТест.ОжидаетЧто(ТаблицаРезультатов)
  .ИмеетТип("Массив")
  .ИмеетДлину(3)
  .КаждыйЭлементСоответствуетПредикату(ЮТест.Предикат()
    .Реквизит("Товар").Заполнено().ИмеетТип("СправочникСсылка.Товары")
    .Реквизит("Период").Заполнено().ИмеетТип("Дата")
    .Реквизит("Количество").Заполнено().ИмеетТип("Число")
    .Реквизит("Цена").Заполнено().ИмеетТип("Число")
  );

ЮТест.ОжидаетЧто(ТаблицаРезультатов)
  .Содержит(ЮТест.Предикат()
    .Реквизит("Товар").Равно(Товар1)) // Таблица содержит строку с Товар1
  .Содержит(ЮТест.Предикат(Новый Структура("Товар", Товар2))) // Таблица содержит строку с Товар2
  .Содержит(ЮТест.Предикат()
    .Реквизит("Товар").Равно(Товар3)  // Таблица содержит строку с Товар3
    .Реквизит("Количество").Равно(2)) // и количеством 2
  
```
